const OBJECT_SSTIDAL = 1
const OBJECT_SAILOR = 2
const OBJECT_REPORTER_F = 7
const OBJECT_REPORTER_M = 8
const OBJECT_P2_SCIENTIST = 9
const OBJECT_SHADY_BOAT_MAN = 22
const VAR_TEMP_FIRST_INTERACT_STATE = VAR_TEMP_0

mapscripts CasteliaCity_LibertyPier_MapScripts {}

script CasteliaCity_SailorInteract {
    lock
    faceplayer
    if (flag(FLAG_SEEN_P2_LAB_SCIENTIST_INTERVIEW) && !flag(FLAG_SPOKEN_TO_SAILOR_ABOUT_P2_LAB)) {
        setflag(FLAG_SPOKEN_TO_SAILOR_ABOUT_P2_LAB)
        msgbox(format("I just saw a man in a white coat in a huge hurry to board a small boat on the other side of the dock."
        "They sped out of the port at a very dangerous speed too.\p"
        ".......\p"
        "He was working for Team Plasma on something suspicious...?\p"
        "I see... I don't know what Team Plasma is, but if you're intent on following, just give me the word and I'll take you to wherever they were headed."), MSGBOX_DEFAULT)
    }
    msgbox(format("Let me know when you are ready to depart."), MSGBOX_DEFAULT)
    dynmultichoice(1, 1, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Let's go", "Stay")
    switch (var(VAR_RESULT)) {
        case 0:
            call(CasteliaCity_AskHomeOrCasteliaCity)
    }
    release
}

script CasteliaCity_AskHomeOrCasteliaCity {
    dynmultipush("Liberty Garden", 0)
    if (flag(FLAG_SEEN_P2_LAB_SCIENTIST_INTERVIEW)) {
        msgbox(format("Do you want to head to Liberty Garden, the warehouse on Route 17, or back home to Lilycove?\p"
        "What do you reckon?"), MSGBOX_DEFAULT)
        dynmultipush("Unova Route 17", 2)
    }
    else {
        msgbox(format("Do you want to go to Liberty Garden not far west of here?\p"
        "Or do you want to head back to Lilycove City in Hoenn.\p"
        "What do you reckon?"), MSGBOX_DEFAULT)
    }
	dynmultipush("Lilycove City", 1)
	dynmultistack(1, 1, FALSE, 4, FALSE, 0, DYN_MULTICHOICE_CB_NONE)

    switch (var(VAR_RESULT)) {
        case 0:
            msgbox(format("Alright, off to Liberty Garden. We're setting sail!"), MSGBOX_NPC)
            call(CasteliaCity_DepartWithSailor)
            warp(MAP_LIBERTY_GARDEN_EXTERIOR, 6, 15)
	        waitstate
        case 1:
            msgbox(format("We've a few days travel ahead of us! All aboard for the Hoenn region!"), MSGBOX_NPC)
            call(CasteliaCity_DepartWithSailor)
            warp(MAP_LILYCOVE_CITY_HARBOR, 8, 11)
	        waitstate
        case 2:
            msgbox(format("Let me pull out my old Unova map. Hopefully it's still up to date. All aboard!"), MSGBOX_NPC)
            call(CasteliaCity_DepartWithSailor)
            warp(MAP_UNOVA_ROUTE17, 52, 79)
	        waitstate
    }
}

script CasteliaCity_DepartWithSailor {
    applymovement(OBJECT_SAILOR, Common_Movement_WalkInPlaceFasterLeft)
    waitmovement(0)
    delay(30)
    hideobjectat(OBJECT_SAILOR, MAP_CASTELIA_CITY_LIBERTY_PIER)
    switch (var(VAR_FACING)) {
        case DIR_NORTH: applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
        case DIR_EAST: applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
        case DIR_SOUTH: applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
    }
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
    waitmovement(0)
    hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_CASTELIA_CITY_LIBERTY_PIER)
    delay(30)
	call(CasteliaCity_FerryDepart)
}

script CasteliaCity_FerryDepart {
    delay(60)
    playse(SE_RG_SS_ANNE_HORN)
    applymovement(OBJECT_SSTIDAL, CasteliaCity_FerryMovement)
    waitmovement(0)
}

movement CasteliaCity_FerryMovement {
    walk_slow_left
	walk_slow_left
	walk_slow_left
	walk_left
	walk_left
	walk_left
	walk_left
}

script CasteliaCity_LibertyPierSign { msgbox(format("Castelia City\nLiberty Pier"), MSGBOX_SIGN) }
script CasteliaCity_FleaMarketsSign { msgbox(format("Castelia City's Flea Markets!\nPermanently set up for all your second-hand shopping needs!"), MSGBOX_SIGN) }
script CasteliaCity_StreetSign { msgbox(format("{LEFT_ARROW} Liberty Pier\n  Narrow Street {RIGHT_ARROW}"), MSGBOX_SIGN) }

script CasteliaCity_PierMan { msgbox(format("My goodness, this sure is a magnificent view. Its one of Castelia City's finest."), MSGBOX_NPC) }
script CasteliaCity_PierBoy { msgbox(format("Hey man, check out all of those sick boats aye. That's heaps good, yeah."), MSGBOX_NPC) }
script CasteliaCity_MarketLittleGirl { msgbox(format("Wow, there are so many things! Look at all the baloons!"), MSGBOX_NPC) }
script CasteliaCity_MarketGirl { msgbox(format("I could really do with an extra pair of shoes...\pOh, but maybe I would be better off with this nice scented candle for my apartment..."), MSGBOX_NPC) }
script CasteliaCity_MarketMan2 { msgbox(format("This area of Castelia City was famous for its seasonal flea markets.\p"
"Perhaps it was due to the beautiful view, or the refreshing sea breeze. Most likely both.\p"
"Eventually, it just became so popular that Castelia's Gym Leader Burgh made it a year round attraction for the City.\p"
"He is, after all, an avid attendee of course."), MSGBOX_NPC) }
script CasteliaCity_MarketPikachu {
    playmoncry(SPECIES_PIKACHU, CRY_MODE_NORMAL) 
    msgbox(format("Pika... pikaaaa!"), MSGBOX_NPC) 
}
script CasteliaCity_CafeSonataLady { msgbox(format("Oh hello there youngster. Wondering where I got this coffee from?\p"
    "A café that I visit often, just down the road here and up a narrow street, it's easy to miss."), MSGBOX_NPC) 
}
script CasteliaCity_ShadyBoatMan { 
    msgbox(format("This is a private boat, and I am waiting for someone important.\p"
    "Access is restricted for the likes of you."), MSGBOX_NPC) 
    applymovement(OBJECT_SHADY_BOAT_MAN, Common_Movement_FaceRight)
    waitmovement(0)
}

script CasteliaCity_MarketMan1 { 
    lock
    if (flag(FLAG_GIVEN_CASTELIA_MARKET_MAN_RAGE_CANDY_BAR)) {
        msgbox(format("...om nom, nom..."), MSGBOX_NPC)
    }
    else {
        msgbox(format("I wonder if anyone here sells Rage Candy Bars?\pOh man I could use one right about now."), MSGBOX_NPC) 
        checkitem(ITEM_RAGE_CANDY_BAR, 1)
        if (var(VAR_RESULT)) {
            msgbox(format("No way, do I spot one poking out of your bag right there?\p"
            "Here, I've got this weird colourful rock. You can have it in return for that delicious candy bar?"), MSGBOX_YESNO) 
            if (var(VAR_RESULT) == YES) {
                setflag(FLAG_GIVEN_CASTELIA_MARKET_MAN_RAGE_CANDY_BAR)
                msgbox(format("AH YES! What a tasty Rage Candy Bar! Thank you. Here is the nice rock."), MSGBOX_NPC)
                removeitem(ITEM_RAGE_CANDY_BAR, 1)
                giveitem(ITEM_SALAMENCITE, 1)
            }
            else {
                msgbox(format("Aww man, thats such a bummer..."), MSGBOX_NPC)
            }
        }
    }
    release
}

script CasteliaCity_Nurse {
    lock
    faceplayer
    msgbox(format("Hello, I'm the Pokémon nurse for the Liberty Pier flea markets. Would you like me to heal your Pokémon?"), MSGBOX_YESNO)
    if (var(VAR_RESULT)) {
        msgbox(format("Let me take your Pokémon for a moment..."), MSGBOX_DEFAULT)
        fadescreen(FADE_TO_BLACK)
        playfanfare(MUS_HEAL)
        waitfanfare
        special(HealPlayerParty)
        fadescreen(FADE_FROM_BLACK)
        delay(30)
        msgbox(format("Thank you for waiting. Your Pokémon have been fully healed.\lPlease come again."), MSGBOX_DEFAULT)
    }
    else {
        msgbox(format("Please come again."), MSGBOX_DEFAULT)
    }
    release
}

script CasteliaCity_MarketSaleslady {
    lock
    faceplayer
    msgbox(format("Hello hello! Please take a look at my fine items."), MSGBOX_DEFAULT)
    pokemart(CasteliaCity_FleaMarketLadyMart)
    release
}

script CasteliaCity_FleaMarketGirl {
    lock
    faceplayer
    msgbox(format("Hi there, please have a look around and enjoy yourself!"), MSGBOX_DEFAULT)
    pokemart(CasteliaCity_FleaMarketGirlMart)
    release
}

mart CasteliaCity_FleaMarketLadyMart {
    ITEM_POKE_BALL
    ITEM_GREAT_BALL
    ITEM_ULTRA_BALL
    ITEM_CASTELIACONE
    ITEM_RAGE_CANDY_BAR
    ITEM_SWEET_HEART
    ITEM_ROCKY_HELMET
    ITEM_PP_MAX
    ITEM_PROTECTIVE_PADS
    ITEM_ASSAULT_VEST
    ITEM_BLUNDER_POLICY
    ITEM_EJECT_BUTTON
    ITEM_EJECT_PACK
    ITEM_RED_CARD
}

mart CasteliaCity_FleaMarketGirlMart {
    ITEM_MEGA_RING
    ITEM_SCEPTILITE
    ITEM_SWAMPERTITE
    ITEM_BLAZIKENITE
    ITEM_GARDEVOIRITE
    ITEM_GALLADITE
    ITEM_AGGRONITE
    ITEM_MEDICHAMITE
    ITEM_MAWILITE
    ITEM_GLALITITE
    ITEM_MANECTITE
    ITEM_BANETTITE
    ITEM_ABSOLITE
    ITEM_LATIASITE
    ITEM_LATIOSITE
    ITEM_SHARPEDONITE
    ITEM_CAMERUPTITE
    ITEM_ALTARIANITE
    ITEM_METAGROSSITE
}

script CasteliaCity_InterviewerF { 
    lock
    if (!flag(FLAG_SEEN_P2_LAB_SCIENTIST_INTERVIEW)) {
        msgbox(format("Quickly! There he is!"), MSGBOX_DEFAULT) 
        closemessage
        goto(CasteliaCity_P2ScientistInterview)
    }
    else {
        msgbox(format("He ran off...\nClearly he is involved in something fishy...\p"
        "We had reports of a warehouse on Route 17 being used by Team Plasma to hide suspicious activity."), MSGBOX_NPC) 
    }
    release
}

script CasteliaCity_InterviewerM {
    lock
    if (!flag(FLAG_SEEN_P2_LAB_SCIENTIST_INTERVIEW)) {
        msgbox(format("Three...\pTwo...\pOne...\pCamera's rolling!"), MSGBOX_DEFAULT) 
        closemessage
        goto(CasteliaCity_P2ScientistInterview)
    }
    else {
        msgbox(format("What an absolute donkey..."), MSGBOX_NPC) 
    }
    release
}

script CasteliaCity_P2Scientist { 
    lock
    msgbox(format(".......\p"
    "He seems to be on an important sounding phone call. Better leave him alone..."), MSGBOX_DEFAULT) 
    closemessage
    delay(30)
    goto(CasteliaCity_P2ScientistInterview)
    release
}

script CasteliaCity_P2ScientistInterview {
    lock
    special(Script_FadeOutMapMusic)

    // Set a temp var based on where the player starts the interview.
    if (var(VAR_LAST_TALKED) == OBJECT_REPORTER_F) {
        switch (var(VAR_FACING)) {
            case DIR_NORTH: setvar(VAR_TEMP_FIRST_INTERACT_STATE, 0)
            case DIR_SOUTH: setvar(VAR_TEMP_FIRST_INTERACT_STATE, 1)
            case DIR_EAST: setvar(VAR_TEMP_FIRST_INTERACT_STATE, 2)
        }
    }
    elif (var(VAR_LAST_TALKED) == OBJECT_REPORTER_M) {
        switch (var(VAR_FACING)) {
            case DIR_NORTH: setvar(VAR_TEMP_FIRST_INTERACT_STATE, 3)
            case DIR_EAST: setvar(VAR_TEMP_FIRST_INTERACT_STATE, 4)
            case DIR_WEST: setvar(VAR_TEMP_FIRST_INTERACT_STATE, 5)
        }
    }
    elif (var(VAR_LAST_TALKED) == OBJECT_P2_SCIENTIST) {
        switch (var(VAR_FACING)) {
            case DIR_NORTH: setvar(VAR_TEMP_FIRST_INTERACT_STATE, 6)
            case DIR_SOUTH: setvar(VAR_TEMP_FIRST_INTERACT_STATE, 7)
            case DIR_EAST: setvar(VAR_TEMP_FIRST_INTERACT_STATE, 8)
            case DIR_WEST: setvar(VAR_TEMP_FIRST_INTERACT_STATE, 9)
        }
    }

    // Decide how to move player then move reporters to the scientist
    switch (var(VAR_TEMP_FIRST_INTERACT_STATE)) {
        case 0: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterF_North_1)
        case 1: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterF_South_1)
        case 2: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterF_East_1)
        case 3: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterM_North_1)
        case 4: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterM_East_1)
        case 5: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterM_West_1)
        case 6: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_Scientist_North_1)
        // Apply 7 after reporter movement to simulate a shove
        case 8: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_Scientist_East_1)
        // Apply 9 after reporter movement to simulate a shove
        default: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_None)
    }

    applymovement(OBJECT_REPORTER_F, CasteliaCity_P2ScientistInterview_ReporterF_MoveToInterview)
    applymovement(OBJECT_REPORTER_M, CasteliaCity_P2ScientistInterview_ReporterM_MoveToInterview)
    waitmovement(OBJECT_REPORTER_F)
    waitmovement(OBJECT_REPORTER_M)
    waitmovement(OBJ_EVENT_ID_PLAYER)

    switch (var(VAR_TEMP_FIRST_INTERACT_STATE)) {
        case 7: playse(SE_WALL_HIT) applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_Scientist_South_1)
        case 9: playse(SE_WALL_HIT) applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_Scientist_West_1)
        default: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_None)
    }
    waitmovement(OBJ_EVENT_ID_PLAYER)

    playse(SE_PIN)
    applymovement(OBJECT_P2_SCIENTIST, CasteliaCity_P2ScientistInterview_Scientist_Startled)
    waitmovement(OBJECT_P2_SCIENTIST)
    delay(30)

    switch (var(VAR_TEMP_FIRST_INTERACT_STATE)) {
        case 0: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterF_North_2)
        case 1: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterF_South_2)
        case 2: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterF_East_2)
        case 3: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterM_North_2)
        case 4: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterM_East_2)
        case 5: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_ReporterM_West_2)
        case 6: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_Scientist_North_2)
        case 7: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_Scientist_South_2)
        case 8: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_Scientist_East_2)
        case 9: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_Scientist_West_2)
        default: applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_None)
    }
    waitmovement(OBJ_EVENT_ID_PLAYER)

    delay(10)
    applymovement(OBJECT_REPORTER_F, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    delay(10)
    msgbox(format("...Mr. Dudley! What can you tell us about the rumours of a secret experiment you're allegedly undertaking for Team Plasma?"), MSGBOX_DEFAULT) 
    delay(10)
    applymovement(OBJECT_P2_SCIENTIST, Common_Movement_WalkInPlaceFasterRight)
    waitmovement(0)
    applymovement(OBJECT_P2_SCIENTIST, Common_Movement_WalkInPlaceFasterRight)
    waitmovement(0)
    applymovement(OBJECT_P2_SCIENTIST, Common_Movement_WalkInPlaceFasterRight)
    waitmovement(0)
    delay(10)
    msgbox(format("You seem to be mistaken! I'm not Dudley.\p"
    "I don't know what you're talking about!"), MSGBOX_DEFAULT) 
    delay(10)
    applymovement(OBJECT_REPORTER_F, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    delay(10)
    msgbox(format("We have our sources. You are no doubt Mr. Dudley.\p"
    "Is there some sort of operation stationed on Route 17, Mr. Dudley? Can you please tell us about that?"), MSGBOX_DEFAULT) 
    playse(SE_PIN)
    applymovement(OBJECT_P2_SCIENTIST, Common_Movement_ExclamationMark)
    waitmovement(0)
    msgbox(format("............\p"
    "Argh! To hell with it!\nI don't have the time to deal with this now!\p"
    "Team Plasma will soon rule all of Unova! Just you wait!"), MSGBOX_DEFAULT) 
    applymovement(OBJECT_P2_SCIENTIST, Common_Movement_FacePlayer)
    waitmovement(0)

    msgbox(format("Now move it kid! I have an important ferry to catch!"), MSGBOX_DEFAULT) 
    closemessage

    setflag(FLAG_MAP_MUSIC_OVERRIDE)
    setvar(VAR_MAP_MUSIC_OVERRIDE, MUS_NONE)
    setflag(FLAG_CUSTOM_BATTLE_MUSIC)
    setvar(VAR_TEMP_CUSTOM_BATTLE_MUSIC, MUS_UNOVA_VS_PLASMA)
    trainerbattle_no_intro(TRAINER_P2DUDLEY1, "My god, what is this nonsense?!")
    clearflag(FLAG_MAP_MUSIC_OVERRIDE)

    msgbox(format("Enough!\p"
    "I know nothing about any warehouse on Route 17.\p"
    "Now out of my way!"), MSGBOX_DEFAULT) 
    closemessage

    if (var(VAR_TEMP_FIRST_INTERACT_STATE) == 8) {
        playse(SE_WALL_HIT)
        applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_Scientist_East_3)
    }
    else {
        applymovement(OBJ_EVENT_ID_PLAYER, CasteliaCity_P2ScientistInterview_None)
    }

    applymovement(OBJECT_P2_SCIENTIST, CasteliaCity_P2ScientistInterview_Scientist_RunAway)
    waitmovement(OBJ_EVENT_ID_PLAYER)       
    delay(20)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
    applymovement(OBJECT_REPORTER_F, Common_Movement_FaceLeft)
    applymovement(OBJECT_REPORTER_M, Common_Movement_FaceLeft)
    waitmovement(OBJECT_P2_SCIENTIST)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    waitmovement(OBJECT_REPORTER_F)
    waitmovement(OBJECT_REPORTER_M)
    
    removeobject(OBJECT_P2_SCIENTIST)
    setflag(FLAG_SEEN_P2_LAB_SCIENTIST_INTERVIEW)

    delay(60)
    playse(SE_RG_SS_ANNE_HORN)
    waitse
    applymovement(OBJECT_REPORTER_F, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(format("Hmm...\nI didn't say anything about a warehouse..."), MSGBOX_DEFAULT) 
    closemessage

    special(Overworld_PlaySpecialMapMusic)
    release
}

movement CasteliaCity_P2ScientistInterview_None { delay_1 }

movement CasteliaCity_P2ScientistInterview_ReporterF_North_1 { player_run_left face_down }
movement CasteliaCity_P2ScientistInterview_ReporterF_South_1 { face_down }
movement CasteliaCity_P2ScientistInterview_ReporterF_East_1 { face_down }
movement CasteliaCity_P2ScientistInterview_ReporterM_North_1 { face_left }
movement CasteliaCity_P2ScientistInterview_ReporterM_East_1 { player_run_left face_down }
movement CasteliaCity_P2ScientistInterview_ReporterM_West_1 { face_down }
movement CasteliaCity_P2ScientistInterview_Scientist_North_1 { face_up }
movement CasteliaCity_P2ScientistInterview_Scientist_South_1 { player_run_left face_right }
movement CasteliaCity_P2ScientistInterview_Scientist_East_1 { face_right }
movement CasteliaCity_P2ScientistInterview_Scientist_West_1 { player_run_down face_up }

movement CasteliaCity_P2ScientistInterview_ReporterF_MoveToInterview {
    walk_fast_down * 3
    face_left
}

movement CasteliaCity_P2ScientistInterview_ReporterM_MoveToInterview {
    walk_fast_left
    walk_fast_down
    walk_fast_left
    face_down
}

movement CasteliaCity_P2ScientistInterview_Scientist_Startled {
    emote_exclamation_mark
    delay_16 * 2
    face_up
}

movement CasteliaCity_P2ScientistInterview_ReporterF_North_2 { walk_left walk_down face_right }
movement CasteliaCity_P2ScientistInterview_ReporterF_South_2 { walk_left * 2 walk_down * 3 face_right}
movement CasteliaCity_P2ScientistInterview_ReporterF_East_2 { walk_left walk_down * 2 face_right }
movement CasteliaCity_P2ScientistInterview_ReporterM_North_2 { walk_up walk_left * 3 walk_down face_right }
movement CasteliaCity_P2ScientistInterview_ReporterM_East_2 { walk_left walk_down face_right }
movement CasteliaCity_P2ScientistInterview_ReporterM_West_2 { walk_left * 4 walk_down face_right }
movement CasteliaCity_P2ScientistInterview_Scientist_North_2 { face_up }
movement CasteliaCity_P2ScientistInterview_Scientist_South_2 { face_right }
movement CasteliaCity_P2ScientistInterview_Scientist_East_2 { face_right }
movement CasteliaCity_P2ScientistInterview_Scientist_West_2 { face_up }

// When scientist runs away
movement CasteliaCity_P2ScientistInterview_Scientist_East_3 { player_run_up face_down }

movement CasteliaCity_P2ScientistInterview_Scientist_RunAway {
    walk_fast_left * 10
}

