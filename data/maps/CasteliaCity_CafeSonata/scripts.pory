const OBJECT_ID_MELOETTA = 2
const OBJECT_ID_BARD = 1
const OBJECT_ID_PIROUETTE_FORM = 6

mapscripts CasteliaCity_CafeSonata_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        hideobjectat(OBJECT_ID_PIROUETTE_FORM, MAP_CASTELIA_CITY_CAFE_SONATA)
        if (flag(FLAG_CAPTURED_MELOETTA) || !flag(FLAG_RELIC_SONG_PLAYED)) {
            setflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
        }
        else {
            clearflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
        }
    }
}

script CafeSonata_Bard {
    lock
    faceplayer
    if (flag(FLAG_RELIC_SONG_PLAYED)) {
        release
        return
    }

    msgbox(format("The scent you brought me reminded me of a long-ago melody from the fringes of my sepia-toned memories.\p"
    "It was a song my mother loved...\nIt makes me feel so nostalgic.\p"
    "Would you like to hear the melody?"), MSGBOX_NPC)
    special(Script_FadeOutMapMusic)
    switch (var(VAR_FACING)) {
        case DIR_EAST: applymovement(OBJ_EVENT_ID_PLAYER, CafeSonata_WalkToSong_FacingEast)
        case DIR_WEST: applymovement(OBJ_EVENT_ID_PLAYER, CafeSonata_WalkToSong_FacingWest)
        default: applymovement(OBJ_EVENT_ID_PLAYER, CafeSonata_WalkToSong_FacingNorth)
    }
    waitmovement(0)
    delay(30)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, CafeSonata_Song_CameraUp)
    waitmovement(0)
    delay(20)

    // Play song
    call(CafeSonata_PlaySong)
    // End song

    applymovement(OBJ_EVENT_ID_CAMERA, CafeSonata_Song_CameraDown)
    waitmovement(0)
    delay(20)
    special(RemoveCameraObject)
    special(Overworld_PlaySpecialMapMusic)
    release
}

script CafeSonata_PlaySong {
    setflag(FLAG_RELIC_SONG_PLAYED)
    clearflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
    addobject(OBJECT_ID_MELOETTA)
    showobjectat(OBJECT_ID_MELOETTA, MAP_CASTELIA_CITY_CAFE_SONATA)
    setobjectxy(OBJECT_ID_MELOETTA, 4, 1)
    lockelevation(OBJECT_ID_MELOETTA, 4)
    playbgm(MUS_CAFE_SONATA_RELIC_SONG, FALSE)
    applymovement(OBJECT_ID_BARD, CafeSonata_Song_Bard)
    delay(180)

    // First few circles
    applymovement(OBJECT_ID_MELOETTA, CafeSonata_Song_MeloettaDance_1)
    waitmovement(OBJECT_ID_MELOETTA)

    // Transform into Pirouette form
    fadescreen(FADE_TO_WHITE)
    hideobjectat(OBJECT_ID_MELOETTA, MAP_CASTELIA_CITY_CAFE_SONATA)
    addobject(OBJECT_ID_PIROUETTE_FORM, MAP_CASTELIA_CITY_CAFE_SONATA)
    showobjectat(OBJECT_ID_PIROUETTE_FORM, MAP_CASTELIA_CITY_CAFE_SONATA)
    setobjectxy(OBJECT_ID_PIROUETTE_FORM, 2, 8)
    lockelevation(OBJECT_ID_PIROUETTE_FORM, 4)
    dofieldeffectsparkle(2, 7, 1)
    dofieldeffectsparkle(2, 8, 1)
    dofieldeffectsparkle(2, 9, 1)
    dofieldeffectsparkle(3, 7, 1)
    dofieldeffectsparkle(2, 9, 1)
    fadescreen(FADE_FROM_WHITE)

    // Pirouette Dance
    applymovement(OBJECT_ID_PIROUETTE_FORM, CafeSonata_Song_MeloettaDance_2)
    waitmovement(OBJECT_ID_PIROUETTE_FORM)

    // Transform back to Aria form
    fadescreen(FADE_TO_WHITE)
    hideobjectat(OBJECT_ID_PIROUETTE_FORM, MAP_CASTELIA_CITY_CAFE_SONATA)
    showobjectat(OBJECT_ID_MELOETTA, MAP_CASTELIA_CITY_CAFE_SONATA)
    setobjectxy(OBJECT_ID_MELOETTA, 2, 10)
    lockelevation(OBJECT_ID_MELOETTA, 4)
    dofieldeffectsparkle(2, 9, 1)
    dofieldeffectsparkle(2, 10, 1)
    dofieldeffectsparkle(2, 11, 1)
    dofieldeffectsparkle(3, 9, 1)
    dofieldeffectsparkle(2, 11, 1)
    fadescreen(FADE_FROM_WHITE)

    // Last few circles
    applymovement(OBJECT_ID_MELOETTA, CafeSonata_Song_MeloettaDance_3)
    waitmovement(OBJECT_ID_MELOETTA)
    
    resetelevation(OBJECT_ID_MELOETTA)
    delay(120)
}

movement CafeSonata_WalkToSong_FacingEast {
    walk_down
    walk_right
    walk_down * 4
    face_up
}

movement CafeSonata_WalkToSong_FacingNorth {
    walk_down * 4
    face_up
}

movement CafeSonata_WalkToSong_FacingWest {
    walk_down
    walk_left
    walk_down * 4
    face_up
}

movement CafeSonata_Song_CameraUp {
    walk_slow_up * 4
}

movement CafeSonata_Song_CameraDown {
    walk_slow_down * 4
}

movement CafeSonata_Song_Bard {
    walk_in_place_slow_down * 50
}

movement CafeSonata_Song_MeloettaDance_1 {
    levitate
    walk_slow_down * 9
    lock_facing_direction
    delay_16 * 8

    // Spin
    force_face_up
    force_face_right
    force_face_down
    force_face_left

    // 1st small slow clockwise circle while rotating clockwise
    walk_slow_diag_northeast
    walk_slow_diag_southeast
    walk_slow_diag_southwest
    walk_slow_left
    walk_slow_left
    walk_slow_diag_northwest
    walk_slow_up

    // 2nd medium clockwise circle a little faster
    walk_diag_northeast
    walk_right
    walk_right
    walk_right
    walk_diag_southeast
    walk_down
    walk_diag_southwest
    walk_diag_southwest
    walk_left
    walk_left
    walk_diag_northwest
    walk_diag_northwest
    walk_up
    walk_diag_northeast
    walk_diag_northeast
    
    // 3rd large clockwise circle at same speed
    walk_right
    walk_right
    walk_diag_southeast
    walk_diag_southeast
    walk_down
    walk_down
    walk_diag_southwest
    walk_diag_southwest
    walk_left
    walk_left
    walk_diag_northwest
    walk_diag_northwest
    walk_up
    walk_up
    walk_diag_northeast

    unlock_facing_direction
    stop_levitate
}

movement CafeSonata_Song_MeloettaDance_2 {
    lock_facing_direction

    walk_right
    walk_fast_right
    walk_fast_right
    walk_right
    levitate
    delay_16
    stop_levitate
    walk_down
    walk_diag_southwest
    walk_fast_down
    walk_down
    levitate
    delay_16
    stop_levitate
    walk_diag_northwest
    walk_fast_up
    walk_diag_northwest
    levitate
    delay_16
    stop_levitate
    walk_left
    walk_fast_left
    walk_diag_southwest
    levitate
    delay_16
    stop_levitate
    walk_diag_southeast
    walk_fast_right
    walk_right
    levitate
    delay_16
    stop_levitate
    walk_diag_northeast
    walk_fast_right
    walk_diag_northeast
    levitate
    delay_16
    stop_levitate
    walk_diag_southwest
    walk_fast_down
    walk_down
    levitate
    delay_16
    stop_levitate
    walk_diag_northwest
    walk_fast_left
    walk_diag_northwest
    levitate
    delay_16
    stop_levitate

    unlock_facing_direction
}

movement CafeSonata_Song_MeloettaDance_3 {
    levitate
    lock_facing_direction

    // Circle 4
    walk_up
    walk_diag_northeast
    walk_right
    walk_right
    walk_right
    walk_diag_southeast
    walk_down
    walk_diag_southwest
    walk_diag_southwest
    walk_left
    walk_left
    walk_diag_northwest
    walk_diag_northwest
    walk_up
    walk_diag_northeast
    walk_diag_northeast
    
    // Circle 5
    walk_right
    walk_right
    walk_diag_southeast
    walk_diag_southeast
    walk_down
    walk_down
    walk_diag_southwest
    walk_diag_southwest
    walk_left
    walk_left
    walk_diag_northwest
    walk_diag_northwest
    walk_up
    walk_up
    walk_diag_northeast

    // Return to center
    walk_right
    walk_diag_southeast
    walk_down

    unlock_facing_direction
    face_down
    delay_16 * 4
    stop_levitate
}

script CafeSonata_Meloetta {
    lock
    faceplayer
    waitse
    playmoncry(SPECIES_MELOETTA, CRY_MODE_ENCOUNTER)
    msgbox(format("Ta-ta-ta-tah!"), MSGBOX_NPC)
    delay(20)
    waitmoncry
    setwildbattle(SPECIES_MELOETTA, 40)
    special(BattleSetup_StartLegendaryBattle)
    waitstate
    specialvar(VAR_RESULT, GetBattleOutcome)
    switch (var(VAR_RESULT)) {
        case B_OUTCOME_CAUGHT:
            setflag(FLAG_CAPTURED_VICTINI)
            setflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
            call(Common_EventScript_RemoveStaticPokemon)
        case B_OUTCOME_RAN:
            msgbox(format("Meloetta is watching carefully."), MSGBOX_DEFAULT)
        default:
            fadescreenswapbuffers(FADE_TO_BLACK)
            removeobject(OBJECT_ID_MELOETTA)
            fadescreenswapbuffers(FADE_FROM_BLACK)
            msgbox(format("Meloetta fled behind the furniture..."), MSGBOX_DEFAULT)
    }
    release
    end
}
