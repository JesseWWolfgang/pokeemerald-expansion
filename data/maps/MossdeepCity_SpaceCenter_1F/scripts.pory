const OBJECT_LAUNCH_COUNTER_SCIENTIST = 1
const OBJECT_SCIENTIST_2 = 2
const OBJECT_STEVEN = 11
const OBJECT_OLDMAN = 4
const OBJECT_SAILOR = 3

const WARP_IN_RETURN_X = 7
const WARP_IN_RETURN_Y = 4

mapscripts MossdeepCity_SpaceCenter_1F_MapScripts {
    MAP_SCRIPT_ON_LOAD: MossdeepCity_SpaceCenter_1F_OnLoad
	MAP_SCRIPT_ON_TRANSITION: MossdeepCity_SpaceCenter_1F_OnTransition
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_TEMP_LOADED, 0 {
			setvar(VAR_TEMP_LOADED, 1)
			getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
			if (var(VAR_TEMP_0) == WARP_IN_RETURN_X && var(VAR_TEMP_1) == WARP_IN_RETURN_Y) {
				call(MossdeepCity_SpaceCenter_1F_ReturnedFromSpaceSetup)
			}
		}
	]
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_LOADED, 1 {
            setvar(VAR_TEMP_LOADED, 2)
            getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
            if (var(VAR_TEMP_0) == WARP_IN_RETURN_X && var(VAR_TEMP_1) == WARP_IN_RETURN_Y) {
                call(MossdeepCity_SpaceCenter_1F_ReturnedFromSpace)
            }
        }
    ]
}

script MossdeepCity_SpaceCenter_1F_EventScript_RocketLaunchCounter {
	lock
	faceplayer
	if (flag(FLAG_TALKED_TO_WHITE_ROCK)) {
		if (flag(FLAG_SPOKEN_ABOUT_MILLENNIUM_COMET)) {
			call(MossdeepCity_SpaceCenter_1F_ReadyForSpace)
		}
		else {
			call(MossdeepCity_SpaceCenter_1F_ShowWhiteRock)
		}
		release
		end
	}

	goto_if_eq(VAR_MOSSDEEP_CITY_STATE, 2, MossdeepCity_SpaceCenter_1F_EventScript_RocketLaunchCounterMagma)
	dotimebasedevents
	specialvar(VAR_RESULT, GetWeekCount)
	buffernumberstring(STR_VAR_1, VAR_RESULT)
	call_if_eq(VAR_RESULT, 0, MossdeepCity_SpaceCenter_1F_EventScript_NoLaunchesYet)
	call_if_ge(VAR_RESULT, 1, MossdeepCity_SpaceCenter_1F_EventScript_ShowLaunchNumber)
	closemessage
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script MossdeepCity_SpaceCenter_1F_ShowWhiteRock {
	setflag(FLAG_SPOKEN_ABOUT_MILLENNIUM_COMET)
	buffernumberstring(STR_VAR_1, 100)
	call(MossdeepCity_SpaceCenter_1F_EventScript_ShowLaunchNumber)
	playse(SE_PIN)
	applymovement(OBJECT_LAUNCH_COUNTER_SCIENTIST, Common_Movement_ExclamationMark)
	waitmovement(0)
	waitse
	msgbox(format("Is that... a piece of the white rock just outside the space center?\p"
		"It's not? Goodness it is remarkably similar!\p"
		"This is incredible, these stones are very special to us here.\p"
		"They are small fragments of the Millennium Comet, a pure-white comet that is seen for only a single week every thousand years.\p"
		"You might have heard this is due to happen very soon.\p"
		"You've seen it on TV right?\p"
		"The Milennium Festival where the comet is visible at night is being prepared in the coming weeks..."), MSGBOX_DEFAULT)
	playse(SE_PIN)
	applymovement(OBJECT_SCIENTIST_2, Common_Movement_ExclamationMark)
	applymovement(OBJECT_SCIENTIST_2, Common_Movement_FaceRight)
	waitmovement(0)
	waitse
	delay(20)
	applymovement(OBJECT_SCIENTIST_2, MossdeepCity_SpaceCenter_1F_Scientist2ApproachPlayer)
	waitmovement(0)
	setvar(VAR_TARGET_OBJECT_EVENT, OBJECT_SCIENTIST_2)
	if (var(VAR_FACING) == DIR_EAST) {
		applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
	}
	elif (var(VAR_FACING) == DIR_WEST) {
		applymovement(OBJ_EVENT_ID_PLAYER, MossdeepCity_SpaceCenter_1F_PlayerMoveToScientist2)
	}
	applymovement(OBJECT_SCIENTIST_2, Common_Movement_WalkRight)
	waitmovement(OBJECT_SCIENTIST_2)
	waitmovement(OBJ_EVENT_ID_PLAYER)
	playerfaceobject(OBJECT_SCIENTIST_2)
	faceobject(OBJECT_LAUNCH_COUNTER_SCIENTIST, OBJECT_SCIENTIST_2)
	delay(20)
	msgbox(format("I couldn't help but overhear you pair talking about the Millennium Comet.\p"
		"We have performed many geological studies on the white rock here in Mossdeep.\p"
		"No other fragments have ever been reported showing up on Earth except the one right here."), MSGBOX_DEFAULT)
	objectfaceplayer(OBJECT_SCIENTIST_2)
	msgbox(format("And now of course this one you have right here...\p"
		"To deepen the mystery, we also discovered minerals in the white rock that we have never identified in any other recorded comet.\p"
		"Many of us here at the space center believe that the Millennium Comet is something very special."), MSGBOX_DEFAULT)
	delay(20)
	objectfaceplayer(OBJECT_LAUNCH_COUNTER_SCIENTIST)
	playerfaceobject(OBJECT_LAUNCH_COUNTER_SCIENTIST)
	applymovement(OBJECT_LAUNCH_COUNTER_SCIENTIST, Common_Movement_WalkInPlace)
	waitmovement(0)
	delay(20)
	msgbox(format("Showing up right here at the space center now of all times is very bizarre...\p"
		"With the festival just around the corner, the 100th successful rocket launch, and the comet approaching very soon...\p"
		"I think it can't just be a coincidence.\p"
		".........\p"
		"The comet is due to pass very close to our moon in about 2 weeks time.\p"
		"You should join the spacecraft crew on their next journey to the moon for the event.\p"
		"I'm not kidding either, this is truly a once in a lifetime opportunity to try and unravel the secrets the comet must hold.\p"
		"It must either be destiny that brought you here today, or a long held wish from the cosmos..."), MSGBOX_DEFAULT)
	delay(20)
	applymovement(OBJECT_SCIENTIST_2, Common_Movement_WalkInPlaceDown)
	waitmovement(0)
	playerfaceobject(OBJECT_SCIENTIST_2)
	delay(20)
	checkplayergender
	if (var(VAR_RESULT) == MALE) {
		msgbox(format("What do you say young man?"), MSGBOX_DEFAULT)
	}
	else {
		msgbox(format("What do you say young lady?"), MSGBOX_DEFAULT)
	}
	msgbox(format("Would you like to join the crew on the next expedition to the moon?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		msgbox(format("You will be able to help us out with some incredible research. You should be proud!\p"
			"Our team will help arrange the paperwork and prepare you for space travel.\p"
			"Please return here as soon as you are ready for take-off."), MSGBOX_DEFAULT)
	}
	else {
		msgbox(format("I understand. It's a huge decision to make, travelling into space. Think it over and come back and talk to us if you decide to join."), MSGBOX_DEFAULT)
	}
}

movement MossdeepCity_SpaceCenter_1F_Scientist2ApproachPlayer { walk_right * 3 }
movement MossdeepCity_SpaceCenter_1F_PlayerMoveForScientist2 { walk_down face_object }
movement MossdeepCity_SpaceCenter_1F_PlayerMoveToScientist2 { walk_down walk_left }

script MossdeepCity_SpaceCenter_1F_ReadyForSpace {
	msgbox(format("Have you prepared yourself for the trip to the moon?\p"
	"Once we take off, we won't be returning until we have completed the expedition or the mission is cancelled.\p"
	"Additionally, we will have NO connection to the Pokémon Storage System, so make sure you have your best team with you.\p"
	"Are you ready?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == NO) {
		msgbox(format("Please get yourself ready and return ASAP, you won't want to miss this."), MSGBOX_DEFAULT)
		return
	}

	msgbox(format("I will go and find one of our guides, they will show you to the launch pad. Please wait here a moment."), MSGBOX_DEFAULT)
	warpdirection(DIR_NORTH, MAP_MOSSDEEP_CITY_SPACE_CENTER_PAD, 0)
	waitstate
}

script MossdeepCity_SpaceCenter_1F_ReturnedFromSpaceSetup {
	// If Steven is hidden, remember that he needs to be re-hidden and show him temporarily.
	clearflag(FLAG_TEMP_1)
	if (flag(FLAG_HIDE_MOSSDEEP_CITY_SPACE_CENTER_1F_STEVEN)) {
		setflag(FLAG_TEMP_1)
		clearflag(FLAG_HIDE_MOSSDEEP_CITY_SPACE_CENTER_1F_STEVEN)
		addobject(OBJECT_STEVEN)
	}

	setobjectxy(OBJECT_STEVEN, 8, 2)
	setobjectxy(OBJECT_SCIENTIST_2, 6, 2)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
	turnobject(OBJECT_STEVEN, DIR_SOUTH)
	turnobject(OBJECT_SCIENTIST_2, DIR_SOUTH)
	turnobject(OBJECT_LAUNCH_COUNTER_SCIENTIST, DIR_SOUTH)
	turnobject(OBJECT_OLDMAN, DIR_WEST)
	turnobject(OBJECT_SAILOR, DIR_NORTH)
}

script MossdeepCity_SpaceCenter_1F_ReturnedFromSpace {
	lockall

	msgbox(format("Well done {PLAYER} on a successful mission!\p"
	"You played an instrumental role in our research, proving beyond a shadow of a doubt that the Millennium Comet holds amazing secrets."), MSGBOX_DEFAULT)
	closemessage
	delay(20)

	applymovement(OBJECT_STEVEN, Common_Movement_WalkInPlace)
	waitmovement(0)
	if (flag(FLAG_JIRACHI_WISH_PERFECT_BIDOOF)) {
		msgbox(format("Take good care of that Jirachi {PLAYER}.\p"
		"I hope your wish comes true.\lI'm certain it will."), MSGBOX_DEFAULT)
	}
	else {
		msgbox(format("Take good care of that Jirachi {PLAYER}.\p"
		"I could see in the footage, your wish was beautiful.\lCherish that moment."), MSGBOX_DEFAULT)
	}
	closemessage
	delay(20)

	applymovement(OBJECT_LAUNCH_COUNTER_SCIENTIST, Common_Movement_WalkInPlace)
	waitmovement(0)
	msgbox(format("We at the institute have a few gifts for you to commemorate this significant achievement,\land of course, as a personal thank-you from us..."), MSGBOX_DEFAULT)
	closemessage
	delay(16)
	applymovement(OBJECT_SCIENTIST_2, MossdeepCity_SpaceCenter_1F_Scientist2GivePlayerGift)
	waitmovement(0)
	delay(30)
	giveitem(ITEM_BEAST_BALL, 20)
	giveitem(ITEM_ABILITY_PATCH, 3)
	giveitem(ITEM_LOADED_DICE)
	closemessage
	delay(20)
	applymovement(OBJECT_SCIENTIST_2, MossdeepCity_SpaceCenter_1F_Scientist2MoveBackAfterGift)
	waitmovement(0)
	msgbox(format("I hope you get some good use out of these.\p"
	"We will get back to our usual business after a well-deserved rest.\p"
	"Thank you again {PLAYER}.\lWe hope to see you around."), MSGBOX_DEFAULT)
	closemessage
	
	if (flag(FLAG_JIRACHI_WISH_PERFECT_BIDOOF)) {
		fadeoutbgm(0)
		playse(SE_PIN)
		applymovement(OBJECT_OLDMAN, MossdeepCity_SpaceCenter_1F_OldManTalkOnPhone)
		waitmovement(0)
		delay(90)
		applymovement(OBJECT_OLDMAN, MossdeepCity_SpaceCenter_1F_OldManMentionsHighEnergy)
		waitmovement(0)

		msgbox(format("Very sorry to interrupt..."), MSGBOX_DEFAULT)
		closemessage
		delay(8)
		turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
		turnobject(OBJECT_STEVEN, DIR_EAST)
		turnobject(OBJECT_SCIENTIST_2, DIR_EAST)
		turnobject(OBJECT_LAUNCH_COUNTER_SCIENTIST, DIR_EAST)

		applymovement(OBJECT_OLDMAN, Common_Movement_WalkInPlace)
		waitmovement(0)
		msgbox(format("I've just received an urgent message from the weather institute.\p"
		"A few minutes ago, radars started recording unprecedented levels of energy radiating from a landmass to the far east of Hoenn."), MSGBOX_DEFAULT)
		closemessage

		applymovement(OBJECT_SCIENTIST_2, Common_Movement_WalkInPlace)
		waitmovement(0)
		msgbox(format("Is that so? Is it a powerful weather system?"), MSGBOX_DEFAULT)
		closemessage

		applymovement(OBJECT_OLDMAN, Common_Movement_WalkInPlace)
		waitmovement(0)
		msgbox(format("No it doesn't appear so. They said it's manifesting as intense fluctuations in heat and magnetism.\p"
		"That's not all.\lThe landmass it originates from consists mainly of a harsh desert.\p"
		"It's restricted by governments as it is completely uninhabitable for humans and most foreign Pokémon."), MSGBOX_DEFAULT)
		closemessage

		applymovement(OBJECT_STEVEN, Common_Movement_WalkInPlace)
		waitmovement(0)
		msgbox(format("I'm going to presume that we will be sending out a team of researchers?"), MSGBOX_DEFAULT)
		closemessage

		applymovement(OBJECT_OLDMAN, Common_Movement_WalkInPlace)
		waitmovement(0)
		msgbox(format("Naturally.\nWe are coordinating with the weather institute and assembling a team to travel there."), MSGBOX_DEFAULT)
		closemessage

		applymovement(OBJECT_STEVEN, Common_Movement_WalkInPlaceDown)
		waitmovement(0)
		playerfaceobject(OBJECT_STEVEN)
		msgbox(format("This sounds like another opportunity {PLAYER}!\p"
		"You were the good luck charm for the Space Center on their mission, and it was a flawless success.\p"
		"Why not do the same for the Weather Institute?"), MSGBOX_DEFAULT)
		closemessage
		
		turnobject(OBJECT_STEVEN, DIR_EAST)
		turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
		msgbox(format("They would appreciate your presence on this mission {PLAYER}.\p"
		"Steven could be right, you know. Why don't you go and pay the Weather Institute a visit?"
		"Do it soon. I'm sure they will be preparing as we speak."), MSGBOX_DEFAULT)
		closemessage

		playse(SE_PIN)
		applymovement(OBJECT_STEVEN, MossdeepCity_SpaceCenter_1F_StevenConfused)
		waitmovement(0)
		playerfaceobject(OBJECT_STEVEN)
		msgbox(format("This can't be just a coincidence...\p"
		"Perhaps this is your wish coming to fruition...?"), MSGBOX_DEFAULT)
		closemessage

		fadeinbgm(0)
	}

	// If Steven is supposed to be hidden, hide him again.
	if (flag(FLAG_TEMP_1)) {
		setflag(FLAG_HIDE_MOSSDEEP_CITY_SPACE_CENTER_1F_STEVEN)
	}

	warp(MAP_MOSSDEEP_CITY_SPACE_CENTER_1F, 7, 3)
	waitstate

	release
}

movement MossdeepCity_SpaceCenter_1F_Scientist2GivePlayerGift {
	walk_down
	walk_right
	walk_in_place_down 
}

movement MossdeepCity_SpaceCenter_1F_Scientist2MoveBackAfterGift {
	walk_left
	walk_up
	walk_in_place_down 
}

movement MossdeepCity_SpaceCenter_1F_OldManTalkOnPhone {
	face_up
	emote_exclamation_mark
}


movement MossdeepCity_SpaceCenter_1F_OldManMentionsHighEnergy {
	walk_down
	walk_left
}

movement MossdeepCity_SpaceCenter_1F_StevenConfused {
	face_down
	emote_question_mark
}


raw `
.set LOCALID_SCIENTIST_1, 1
.set LOCALID_SCIENTIST_2, 2
.set LOCALID_SAILOR, 3
.set LOCALID_OLD_MAN, 4
.set LOCALID_WOMAN, 5
.set LOCALID_STAIR_GRUNT, 9

MossdeepCity_SpaceCenter_1F_OnTransition:
	goto_if_eq VAR_MOSSDEEP_CITY_STATE, 2, MossdeepCity_SpaceCenter_1F_EventScript_MoveObjectsForTeamMagma
	end

MossdeepCity_SpaceCenter_1F_EventScript_MoveObjectsForTeamMagma::
	setobjectxyperm LOCALID_SAILOR, 1, 9
	setobjectmovementtype LOCALID_SAILOR, MOVEMENT_TYPE_FACE_RIGHT
	setobjectxyperm LOCALID_WOMAN, 0, 8
	setobjectmovementtype LOCALID_WOMAN, MOVEMENT_TYPE_FACE_RIGHT
	setobjectxyperm LOCALID_OLD_MAN, 1, 6
	setobjectmovementtype LOCALID_OLD_MAN, MOVEMENT_TYPE_FACE_RIGHT
	setobjectxyperm LOCALID_SCIENTIST_1, 3, 4
	setobjectmovementtype LOCALID_SCIENTIST_1, MOVEMENT_TYPE_FACE_RIGHT
	setobjectmovementtype LOCALID_SCIENTIST_2, MOVEMENT_TYPE_FACE_RIGHT
	goto_if_eq VAR_MOSSDEEP_SPACE_CENTER_STAIR_GUARD_STATE, 1, MossdeepCity_SpaceCenter_1F_EventScript_MoveStairGuardLeft
	goto_if_eq VAR_MOSSDEEP_SPACE_CENTER_STAIR_GUARD_STATE, 2, MossdeepCity_SpaceCenter_1F_EventScript_MoveStairGuardDown
	goto_if_eq VAR_MOSSDEEP_SPACE_CENTER_STAIR_GUARD_STATE, 3, MossdeepCity_SpaceCenter_1F_EventScript_MoveStairGuardRight
	end

MossdeepCity_SpaceCenter_1F_EventScript_MoveStairGuardLeft::
	setobjectxyperm LOCALID_STAIR_GRUNT, 12, 2
	end

MossdeepCity_SpaceCenter_1F_EventScript_MoveStairGuardDown::
	setobjectxyperm LOCALID_STAIR_GRUNT, 13, 3
	end

@ Functionally unused. See comment on MossdeepCity_SpaceCenter_1F_EventScript_MoveGruntFromStairsEast
MossdeepCity_SpaceCenter_1F_EventScript_MoveStairGuardRight::
	setobjectxyperm LOCALID_STAIR_GRUNT, 14, 2
	end

MossdeepCity_SpaceCenter_1F_OnLoad:
	goto_if_le VAR_MOSSDEEP_CITY_STATE, 2, MossdeepCity_SpaceCenter_1F_EventScript_SetMagmaNote
	end

MossdeepCity_SpaceCenter_1F_EventScript_SetMagmaNote::
	setmetatile 2, 5, METATILE_Facility_DataPad, TRUE
	return

@ MossdeepCity_SpaceCenter_1F_EventScript_RocketLaunchCounter::
@ 	lock
@ 	faceplayer
@ 	goto_if_eq VAR_MOSSDEEP_CITY_STATE, 2, MossdeepCity_SpaceCenter_1F_EventScript_RocketLaunchCounterMagma
@ 	dotimebasedevents
@ 	specialvar VAR_RESULT, GetWeekCount
@ 	buffernumberstring STR_VAR_1, VAR_RESULT
@ 	call_if_eq VAR_RESULT, 0, MossdeepCity_SpaceCenter_1F_EventScript_NoLaunchesYet
@ 	call_if_ge VAR_RESULT, 1, MossdeepCity_SpaceCenter_1F_EventScript_ShowLaunchNumber
@ 	closemessage
@ 	applymovement VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection
@ 	waitmovement 0
@ 	release
@ 	end

MossdeepCity_SpaceCenter_1F_EventScript_NoLaunchesYet::
	msgbox MossdeepCity_SpaceCenter_1F_Text_RocketLaunchImminent, MSGBOX_DEFAULT
	return

MossdeepCity_SpaceCenter_1F_EventScript_ShowLaunchNumber::
	msgbox MossdeepCity_SpaceCenter_1F_Text_SuccessfulLaunchNumber, MSGBOX_DEFAULT
	return

MossdeepCity_SpaceCenter_1F_EventScript_RocketLaunchCounterMagma::
	dotimebasedevents
	specialvar VAR_RESULT, GetWeekCount
	buffernumberstring STR_VAR_1, VAR_RESULT
	call_if_eq VAR_RESULT, 0, MossdeepCity_SpaceCenter_1F_EventScript_NoLaunchesYetMagma
	call_if_ge VAR_RESULT, 1, MossdeepCity_SpaceCenter_1F_EventScript_ShowLaunchNumberMagma
	closemessage
	applymovement VAR_LAST_TALKED, Common_Movement_WalkInPlaceFasterRight
	waitmovement 0
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_NoLaunchesYetMagma::
	msgbox MossdeepCity_SpaceCenter_1F_Text_HaywireButRocketLaunchImminent, MSGBOX_DEFAULT
	return

MossdeepCity_SpaceCenter_1F_EventScript_ShowLaunchNumberMagma::
	msgbox MossdeepCity_SpaceCenter_1F_Text_HaywireButSuccessfulLaunchNumber, MSGBOX_DEFAULT
	return

MossdeepCity_SpaceCenter_1F_EventScript_Scientist::
	lock
	faceplayer
	goto_if_eq VAR_MOSSDEEP_CITY_STATE, 2, MossdeepCity_SpaceCenter_1F_EventScript_ScientistMagma
	msgbox MossdeepCity_SpaceCenter_1F_Text_RocketLaunchDemandsPerfection, MSGBOX_DEFAULT
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_ScientistMagma::
	msgbox MossdeepCity_SpaceCenter_1F_Text_MagmaHaveSightsOnSpaceCenter, MSGBOX_DEFAULT
	applymovement VAR_LAST_TALKED, Common_Movement_WalkInPlaceFasterRight
	waitmovement 0
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_SunStoneMan::
	lock
	faceplayer
	goto_if_eq VAR_MOSSDEEP_CITY_STATE, 2, MossdeepCity_SpaceCenter_1F_EventScript_SunStoneManMagma
	goto_if_set FLAG_RECEIVED_SUN_STONE_MOSSDEEP, MossdeepCity_SpaceCenter_1F_EventScript_GaveSunStone
	msgbox MossdeepCity_SpaceCenter_1F_Text_FoundThisYouCanHaveIt, MSGBOX_DEFAULT
	giveitem ITEM_SUN_STONE
	goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
	setflag FLAG_RECEIVED_SUN_STONE_MOSSDEEP
	msgbox MossdeepCity_SpaceCenter_1F_Text_HoennFamousForMeteorShowers, MSGBOX_DEFAULT
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_GaveSunStone::
	msgbox MossdeepCity_SpaceCenter_1F_Text_HoennFamousForMeteorShowers, MSGBOX_DEFAULT
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_SunStoneManMagma::
	goto_if_set FLAG_RECEIVED_SUN_STONE_MOSSDEEP, MossdeepCity_SpaceCenter_1F_EventScript_GaveSunStoneMagma
	msgbox MossdeepCity_SpaceCenter_1F_Text_MagmaCantStealFuelTakeThis, MSGBOX_DEFAULT
	giveitem ITEM_SUN_STONE
	goto_if_eq VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull
	setflag FLAG_RECEIVED_SUN_STONE_MOSSDEEP
	msgbox MossdeepCity_SpaceCenter_1F_Text_CantStrollOnBeachWithMagma, MSGBOX_DEFAULT
	applymovement VAR_LAST_TALKED, Common_Movement_WalkInPlaceFasterRight
	waitmovement 0
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_GaveSunStoneMagma::
	msgbox MossdeepCity_SpaceCenter_1F_Text_CantStrollOnBeachWithMagma, MSGBOX_DEFAULT
	applymovement VAR_LAST_TALKED, Common_Movement_WalkInPlaceFasterRight
	waitmovement 0
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_Woman::
	lock
	faceplayer
	goto_if_set FLAG_SYS_GAME_CLEAR, MossdeepCity_SpaceCenter_1F_EventScript_WomanNormal
	goto_if_eq VAR_MOSSDEEP_CITY_STATE, 2, MossdeepCity_SpaceCenter_1F_EventScript_WomanMagma
	goto_if_lt VAR_MOSSDEEP_CITY_STATE, 2, MossdeepCity_SpaceCenter_1F_EventScript_WomanNormal
	goto MossdeepCity_SpaceCenter_1F_EventScript_WomanMagma
	end

MossdeepCity_SpaceCenter_1F_EventScript_WomanNormal::
	msgbox MossdeepCity_SpaceCenter_1F_Text_DidPokemonComeFromSpace, MSGBOX_DEFAULT
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_WomanMagma::
	msgbox MossdeepCity_SpaceCenter_1F_Text_AquaShouldBeatMagma, MSGBOX_DEFAULT
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_OldMan::
	lock
	faceplayer
	goto_if_set FLAG_SYS_GAME_CLEAR, MossdeepCity_SpaceCenter_1F_EventScript_OldManNormal
	goto_if_eq VAR_MOSSDEEP_CITY_STATE, 2, MossdeepCity_SpaceCenter_1F_EventScript_OldManMagma
	goto_if_lt VAR_MOSSDEEP_CITY_STATE, 2, MossdeepCity_SpaceCenter_1F_EventScript_OldManNormal
	goto MossdeepCity_SpaceCenter_1F_EventScript_OldManMagma
	end

MossdeepCity_SpaceCenter_1F_EventScript_OldManNormal::
	msgbox MossdeepCity_SpaceCenter_1F_Text_RocketsBoggleMyMind, MSGBOX_DEFAULT
	closemessage
	applymovement VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection
	waitmovement 0
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_OldManMagma::
	msgbox MossdeepCity_SpaceCenter_1F_Text_MagmaWantsToSpoilMyDream, MSGBOX_DEFAULT
	closemessage
	applymovement VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection
	waitmovement 0
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_Steven::
	lock
	faceplayer
	msgbox MossdeepCity_SpaceCenter_1F_Text_StevenMagmaCantBeAllowedToTakeFuel, MSGBOX_DEFAULT
	applymovement VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection
	waitmovement 0
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_MagmaNote::
	lockall
	msgbox MossdeepCity_SpaceCenter_1F_Text_MagmaIntentToStealNotice, MSGBOX_DEFAULT
	releaseall
	end

MossdeepCity_SpaceCenter_1F_EventScript_Grunt3::
	trainerbattle_single TRAINER_GRUNT_SPACE_CENTER_3, MossdeepCity_SpaceCenter_1F_Text_Grunt3Intro, MossdeepCity_SpaceCenter_1F_Text_Grunt3Defeat
	msgbox MossdeepCity_SpaceCenter_1F_Text_Grunt3PostBattle, MSGBOX_AUTOCLOSE
	end

MossdeepCity_SpaceCenter_1F_EventScript_Grunt1::
	trainerbattle_single TRAINER_GRUNT_SPACE_CENTER_1, MossdeepCity_SpaceCenter_1F_Text_Grunt1Intro, MossdeepCity_SpaceCenter_1F_Text_Grunt1Defeat
	msgbox MossdeepCity_SpaceCenter_1F_Text_Grunt1PostBattle, MSGBOX_AUTOCLOSE
	end

MossdeepCity_SpaceCenter_1F_EventScript_Grunt4::
	trainerbattle_single TRAINER_GRUNT_SPACE_CENTER_4, MossdeepCity_SpaceCenter_1F_Text_Grunt4Intro, MossdeepCity_SpaceCenter_1F_Text_Grunt4Defeat
	msgbox MossdeepCity_SpaceCenter_1F_Text_Grunt4PostBattle, MSGBOX_AUTOCLOSE
	end

MossdeepCity_SpaceCenter_1F_EventScript_Grunt2::
	lock
	faceplayer
	goto_if_set FLAG_DEFEATED_GRUNT_SPACE_CENTER_1F, MossdeepCity_SpaceCenter_1F_EventScript_Grunt2Defeated
	msgbox MossdeepCity_SpaceCenter_1F_Text_Grunt2Intro, MSGBOX_DEFAULT
	trainerbattle_no_intro TRAINER_GRUNT_SPACE_CENTER_2, MossdeepCity_SpaceCenter_1F_Text_Grunt2Defeat
	setflag FLAG_DEFEATED_GRUNT_SPACE_CENTER_1F
	copyobjectxytoperm LOCALID_STAIR_GRUNT
	switch VAR_FACING
	case DIR_WEST, MossdeepCity_SpaceCenter_1F_EventScript_MoveGruntFromStairsWest
#ifdef BUGFIX
	case DIR_EAST, MossdeepCity_SpaceCenter_1F_EventScript_MoveGruntFromStairsEast
#else
	case DIR_WEST, MossdeepCity_SpaceCenter_1F_EventScript_MoveGruntFromStairsEast
#endif
	applymovement LOCALID_STAIR_GRUNT, MossdeepCity_SpaceCenter_1F_Movement_MoveGruntFromStairs
	waitmovement 0
	setvar VAR_MOSSDEEP_SPACE_CENTER_STAIR_GUARD_STATE, 2
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_Grunt2Defeated::
	msgbox MossdeepCity_SpaceCenter_1F_Text_Grunt2PostBattle, MSGBOX_DEFAULT
	release
	end

MossdeepCity_SpaceCenter_1F_EventScript_MoveGruntFromStairsWest::
	applymovement LOCALID_STAIR_GRUNT, MossdeepCity_SpaceCenter_1F_Movement_MoveGruntFromStairsWest
	waitmovement 0
	setvar VAR_MOSSDEEP_SPACE_CENTER_STAIR_GUARD_STATE, 1
	release
	end

@ Functionally unused by mistake. The movement is handled anyway after the switch (see above)
@ This also means VAR_MOSSDEEP_SPACE_CENTER_STAIR_GUARD_STATE can never be 3
MossdeepCity_SpaceCenter_1F_EventScript_MoveGruntFromStairsEast::
	applymovement LOCALID_STAIR_GRUNT, MossdeepCity_SpaceCenter_1F_Movement_MoveGruntFromStairsEast
	waitmovement 0
	setvar VAR_MOSSDEEP_SPACE_CENTER_STAIR_GUARD_STATE, 3
	release
	end

MossdeepCity_SpaceCenter_1F_Movement_MoveGruntFromStairsWest:
	lock_facing_direction
	walk_left
	unlock_facing_direction
	step_end

@ Functionally unused. See above
MossdeepCity_SpaceCenter_1F_Movement_MoveGruntFromStairsEast:
	lock_facing_direction
	walk_right
	unlock_facing_direction
	step_end

@ This was meant to only handle the player facing North, but by mistake(?) it also handles the player facing East
MossdeepCity_SpaceCenter_1F_Movement_MoveGruntFromStairs:
	face_left
	lock_facing_direction
	walk_right
	unlock_facing_direction
	step_end

MossdeepCity_SpaceCenter_1F_Text_RocketLaunchImminent:
	.string "The rocket's launch is imminent!$"

MossdeepCity_SpaceCenter_1F_Text_SuccessfulLaunchNumber:
	.string "The rocket launched safely!\n"
	.string "That's successful launch no. {STR_VAR_1}!$"

MossdeepCity_SpaceCenter_1F_Text_HaywireButRocketLaunchImminent:
	.string "I know that things are a little\n"
	.string "haywire right now, but…\p"
	.string "The rocket's launch is imminent!$"

MossdeepCity_SpaceCenter_1F_Text_HaywireButSuccessfulLaunchNumber:
	.string "I know that things are a little\n"
	.string "haywire right now, but…\p"
	.string "The rocket launched safely!\n"
	.string "That's successful launch no. {STR_VAR_1}!$"

MossdeepCity_SpaceCenter_1F_Text_RocketLaunchDemandsPerfection:
	.string "A rocket launch demands perfection.\n"
	.string "Not even a 1% margin of error is allowed.\p"
	.string "Even if it's 99% okay, the whole thing\n"
	.string "is useless if 1% happens to be no good.\p"
	.string "Despite that, we never stop trying.\n"
	.string "Why? It's a dream that never ends.$"

MossdeepCity_SpaceCenter_1F_Text_MagmaHaveSightsOnSpaceCenter:
	.string "Those MAGMA thugs have their sights\n"
	.string "set on our SPACE CENTER.\p"
	.string "But we can't allow anything that minor\n"
	.string "to interfere with our rocket launch!$"

MossdeepCity_SpaceCenter_1F_Text_FoundThisYouCanHaveIt:
	.string "I was taking a stroll down the beach\n"
	.string "when I found this.\p"
	.string "It's not anything I need, so you can\n"
	.string "have it.$"

MossdeepCity_SpaceCenter_1F_Text_HoennFamousForMeteorShowers:
	.string "The HOENN region has been famous for \n"
	.string "its meteor showers for a long time.$"

MossdeepCity_SpaceCenter_1F_Text_MagmaCantStealFuelTakeThis:
	.string "TEAM MAGMA can't be allowed to steal\n"
	.string "the rocket fuel.\p"
	.string "Oh, by the way, you can have this stone\n"
	.string "I found on the beach.$"

MossdeepCity_SpaceCenter_1F_Text_CantStrollOnBeachWithMagma:
	.string "With TEAM MAGMA around, I guess\n"
	.string "strolls on the beach aren't safe.$"

MossdeepCity_SpaceCenter_1F_Text_DidPokemonComeFromSpace:
	.string "Some people claim that POKéMON came\n"
	.string "from space. Could it be true?$"

MossdeepCity_SpaceCenter_1F_Text_AquaShouldBeatMagma:
	.string "TEAM AQUA should take care of\n"
	.string "TEAM MAGMA!\p"
	.string "But if they did that, TEAM AQUA will\n"
	.string "become bold and brazen, won't they?$"

MossdeepCity_SpaceCenter_1F_Text_RocketsBoggleMyMind:
	.string "A giant chunk of metal bursts through\n"
	.string "the skies and flies into space…\p"
	.string "It boggles my mind!$"

MossdeepCity_SpaceCenter_1F_Text_MagmaWantsToSpoilMyDream:
	.string "A giant chunk of metal bursts through\n"
	.string "the skies and flies into space…\p"
	.string "But TEAM MAGMA wants to spoil\n"
	.string "that dream of mine!\p"
	.string "I'm not having any of that!$"

MossdeepCity_SpaceCenter_1F_Text_StevenMagmaCantBeAllowedToTakeFuel:
	.string "STEVEN: {PLAYER}{KUN}, have you read that\n"
	.string "proclamation already?\p"
	.string "TEAM MAGMA is coming after the rocket\n"
	.string "fuel on this island.\p"
	.string "I don't know what they'd need it for,\n"
	.string "but they can't be allowed to take it.\p"
	.string "I'll keep an eye on things for\n"
	.string "a while longer.\p"
	.string "In the meantime, why don't you go\n"
	.string "check out the town?$"

MossdeepCity_SpaceCenter_1F_Text_Grunt3Intro:
	.string "As promised, we've come for\n"
	.string "the rocket fuel!$"

MossdeepCity_SpaceCenter_1F_Text_Grunt3Defeat:
	.string "Ran out of fuel…$"

MossdeepCity_SpaceCenter_1F_Text_Grunt3PostBattle:
	.string "Don't think you're on a roll just\n"
	.string "because you've beaten me!$"

MossdeepCity_SpaceCenter_1F_Text_Grunt1Intro:
	.string "We gave you fair warning!\n"
	.string "There's nothing sneaky about us!$"

MossdeepCity_SpaceCenter_1F_Text_Grunt1Defeat:
	.string "Grrr…\n"
	.string "We should've used sneaky treachery…$"

MossdeepCity_SpaceCenter_1F_Text_Grunt1PostBattle:
	.string "Okay, I get it already! The next time,\n"
	.string "we'll come unannounced.$"

MossdeepCity_SpaceCenter_1F_Text_Grunt4Intro:
	.string "The rocket fuel the SPACE CENTER has\n"
	.string "in storage--that's what we're after.\p"
	.string "We mean to take every last bit of it!$"

MossdeepCity_SpaceCenter_1F_Text_Grunt4Defeat:
	.string "Please, can you spare some fuel?\n"
	.string "Even a chintzy cup will do!$"

MossdeepCity_SpaceCenter_1F_Text_Grunt4PostBattle:
	.string "What are we going to do with\n"
	.string "the rocket fuel?\p"
	.string "How would I know?\n"
	.string "Ask our leader upstairs!$"

MossdeepCity_SpaceCenter_1F_Text_Grunt2Intro:
	.string "Our leader said no one, but no one,\n"
	.string "gets past me!$"

MossdeepCity_SpaceCenter_1F_Text_Grunt2Defeat:
	.string "Ack! Ack! Aaack!$"

MossdeepCity_SpaceCenter_1F_Text_Grunt2PostBattle:
	.string "Please, tell our leader that\n"
	.string "I never abandoned my post.\l"
	.string "That I stayed to the bitter end…$"

MossdeepCity_SpaceCenter_1F_Text_MagmaIntentToStealNotice:
	.string "This is…\n"
	.string "An intent-to-steal notice?\p"
	.string "“To the staff of the SPACE CENTER:\n"
	.string "How are you? We are doing fine.\p"
	.string "“We will soon visit you to take your\n"
	.string "rocket fuel.\p"
	.string "“Please don't try to stop us.\n"
	.string "We will take the fuel no matter what.\p"
	.string "“Let there be more land!\n"
	.string "                                          TEAM MAGMA”$"
`