const VAR_TEMP_WITNESSED_MILLENNIUM_COMET = VAR_TEMP_0
const FLAG_TEMP_SPOKEN_ABOUT_MILLENNIUM_COMET = FLAG_TEMP_1
const OBJECT_JIRACHI = 1
const OBJECT_SCIENTIST1 = 2
const OBJECT_ROCKET = 3
const OBJECT_SCIENTIST2 = 4

const WARP_MOSSDEEP_X = 7
const WARP_MOSSDEEP_Y = 3

mapscripts LunarSummit_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        if (flag(FLAG_CAPTURED_JIRACHI) || !flag(FLAG_WITNESSED_MILLENNIUM_COMET)) {
            setflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
        }
        else {
            clearflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
        }

        if (flag(FLAG_WITNESSED_MILLENNIUM_COMET)) {
            setvar(VAR_TEMP_WITNESSED_MILLENNIUM_COMET, 1)
        }
        else {
            setvar(VAR_TEMP_WITNESSED_MILLENNIUM_COMET, 0)
        }
    }
}

script LunarSummit_ScientistAtRocket {
    lock
    faceplayer
    if (flag(FLAG_WITNESSED_MILLENNIUM_COMET)) {
        // if (!flag(FLAG_TEMP_SPOKEN_ABOUT_MILLENNIUM_COMET)) {
        //     msgbox(format("Ready to leave?"), MSGBOX_YESNO)
        // }
        msgbox(format("Ready to leave?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
            msgbox(format("Please board the shuttle and wait for lift off."), MSGBOX_DEFAULT)
            closemessage
            call(LunarSummit_PrepareTakeOff)
        }
    }
    else {
        msgbox(format("We haven't finished conducting our research here just yet.\p"
        "You could try and find the highest point around here to get a clear view of the comet."), MSGBOX_DEFAULT)
    }
    release
}

script LunarSummit_PrepareTakeOff {
    applymovement(OBJECT_SCIENTIST1, LunarSummit_PrepareTakeOffScientist1MoveForPlayerAndScientist2)
    waitmovement(0)
    applymovementplayerfacing(OBJ_EVENT_ID_PLAYER, 
        LunarSummit_PrepareTakeOffPlayerBoardNorth, 
        LunarSummit_PrepareTakeOffPlayerBoardNorth, 
        LunarSummit_PrepareTakeOffPlayerBoardEast, 
        LunarSummit_PrepareTakeOffPlayerBoardWest)
    applymovement(OBJECT_SCIENTIST2, LunarSummit_PrepareTakeOffScientist2Board)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_LUNAR_SUMMIT)
    playse(SE_EXIT)
    waitmovement(OBJECT_SCIENTIST2)
    hideobjectat(OBJECT_SCIENTIST2, MAP_LUNAR_SUMMIT)
    playse(SE_EXIT)
    applymovement(OBJECT_SCIENTIST1, LunarSummit_PrepareTakeOffScientist1Board)
    waitmovement(0)
    hideobjectat(OBJECT_SCIENTIST1, MAP_LUNAR_SUMMIT)
    playse(SE_EXIT)
    delay(30)
    msgbox(format("Take off."), MSGBOX_DEFAULT)
    closemessage
    delay(60)
    call(LunarSummit_RocketTakeOff)
    warp(MAP_MOSSDEEP_CITY_SPACE_CENTER_1F, WARP_MOSSDEEP_X, WARP_MOSSDEEP_Y)
	waitstate
}

script LunarSummit_RocketTakeOff {
    // Engine Startup
    playse(SE_M_EARTHQUAKE)
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 2)  // horizontal pan
	setvar(VAR_0x8006, 32) // num shakes
	setvar(VAR_0x8007, 2)  // shake delay
	special(ShakeCamera)
    delay(32)

    // Ignition
    applymovement(OBJECT_ROCKET, LunarSummit_TakeOffRocketWarmUp)
    waitmovement(OBJECT_ROCKET)

    // Lift off
    playse(SE_M_EARTHQUAKE)
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 2)  // horizontal pan
	setvar(VAR_0x8006, 50) // num shakes
	setvar(VAR_0x8007, 2)  // shake delay
    special(ShakeCamera)

    // Begin lift off animation
    special(SpawnCameraObject)
    applymovement(OBJECT_ROCKET, LunarSummit_TakeOffRocketMovement)
    applymovement(OBJ_EVENT_ID_CAMERA, LunarSummit_TakeOffCameraMovement)
    
    // Wait until lift off shake finishes
    waitstate

    // Continue heavy vibrations and speed up
    playse(SE_INTRO_BLAST)
    setvar(VAR_0x8004, 1)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 40) // num shakes
	setvar(VAR_0x8007, 2)  // shake delay
    special(ShakeCamera)
    waitstate

    // Continue light vibrations
    setvar(VAR_0x8004, 1)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 80) // num shakes
	setvar(VAR_0x8007, 1)  // shake delay
    special(ShakeCamera)

    // Wait for speed up sound to complete and play rain sound (since it sounds similar to wind)
    delay(30) 
    playse(SE_RAIN)
    
    // Wait until the animation has finished
    waitmovement(OBJECT_ROCKET)
    waitmovement(OBJ_EVENT_ID_CAMERA)
}

movement LunarSummit_PrepareTakeOffScientist1MoveForPlayerAndScientist2 {
    walk_up * 2
    walk_right
    face_left
}

movement LunarSummit_PrepareTakeOffPlayerBoardNorth { walk_up * 4 }
movement LunarSummit_PrepareTakeOffPlayerBoardEast { walk_right walk_up * 3 }
movement LunarSummit_PrepareTakeOffPlayerBoardWest { walk_left walk_up * 3 }


movement LunarSummit_PrepareTakeOffScientist2Board {
    walk_up * 3
    walk_right * 3
    walk_up * 4
}

movement LunarSummit_PrepareTakeOffScientist1Board {
    walk_left
    walk_up
}

movement LunarSummit_TakeOffRocketWarmUp {
    walk_in_place_up * 5
}

movement LunarSummit_TakeOffRocketMovement {
    walk_slow_up * 5
    walk_up * 8
    walk_fast_up * 16
    walk_faster_up * 22
}

movement LunarSummit_TakeOffCameraMovement {
    walk_slow_up * 5
    walk_up * 8
    walk_fast_up * 16
    walk_faster_up * 10
    walk_fast_up * 3
    walk_up * 2
}

script LunarSummit_WitnessMillenniumComet {
    lock
    setflag(FLAG_WITNESSED_MILLENNIUM_COMET)
    setvar(VAR_TEMP_WITNESSED_MILLENNIUM_COMET, 1)
    delay(60)
    special(SpawnCameraObject)

    // Talk to the scientists first
    call(LunarSummit_WitnessMillenniumComet_ScientistsBefore)

    
    
    delay(120)
    
    // Sparkle
    playse(SE_M_DETECT)
	dofieldeffectsparkle(47, 3, 0)
    dofieldeffectsparkle(46, 3, 0)
    dofieldeffectsparkle(48, 3, 0)
    dofieldeffectsparkle(47, 2, 0)
    dofieldeffectsparkle(47, 4, 0)
	waitfieldeffect(FLDEFF_SPARKLE)
    delay(50)

    // Orb anim
    playfanfare(MUS_AWAKEN_LEGEND)
	playse(SE_ORB)
    setvar(VAR_RESULT, 4)
    special(DoOrbEffect)
    delay(500)
    special(FadeOutOrbEffect)
	waitstate

    // Show Jirachi
    fadescreen(FADE_TO_WHITE)
    clearflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
    addobject(OBJECT_JIRACHI)
    setobjectxy(OBJECT_JIRACHI, 44, 4)
    playse(SE_ICE_BREAK) 
    fadescreen(FADE_FROM_WHITE)
    waitse
    delay(20)
    playmoncry(SPECIES_JIRACHI, CRY_MODE_ENCOUNTER)
    waitmoncry
    delay(60)

    // Approach the player and start the battle
    applymovement(OBJ_EVENT_ID_CAMERA, LunarSummit_Comet_CameraMoveFromComet)
    waitmovement(0)
    delay(20)
    applymovement(OBJECT_JIRACHI, LunarSummit_Comet_JirachiMovesToBattle)
    waitmovement(0)
    special(RemoveCameraObject)
    delay(20)
    setvar(VAR_LAST_TALKED, OBJECT_JIRACHI)
    goto(LunarSummit_Jirachi)
}

script LunarSummit_WitnessMillenniumComet_ScientistsBefore {
    //applymovement(OBJ_EVENT_ID_CAMERA, LunarSummit_Comet_CameraMoveToComet1)
    //waitmovement(0)
    delay(120)

    setobjectxyperm(OBJECT_SCIENTIST1, 44, 14)
    setobjectxyperm(OBJECT_SCIENTIST2, 45, 13)
    applymovement(OBJECT_SCIENTIST1, LunarSummit_Comet_Scientist1ApproachPlayer)
    applymovement(OBJECT_SCIENTIST2, LunarSummit_Comet_Scientist2ApproachPlayer)
    waitmovement(OBJECT_SCIENTIST1)
    waitmovement(OBJECT_SCIENTIST2)
    delay(30)

    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Would you look at that..."), MSGBOX_DEFAULT)
    closemessage
    delay(100)

    msgbox(format("This truly is a once in a lifetime view.\p"
        "I used to read books about this sort of thing when I was just a kid.\p"
        "...but nothing truly prepares you from seeing something like this first-hand."), MSGBOX_DEFAULT)
    closemessage
    delay(60)
    
    applymovement(OBJECT_SCIENTIST2, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Look how bright the comet shines. Simply magnificent.\p"), MSGBOX_DEFAULT)
    closemessage
    delay(40)

    dofieldeffectsparkle(44, 7, 0)
	waitfieldeffect(FLDEFF_SPARKLE)

    applymovement(OBJECT_SCIENTIST1, Common_Movement_ExclamationMark)
    delay(15)
    applymovement(OBJECT_SCIENTIST2, Common_Movement_ExclamationMark)
    waitmovement(OBJECT_SCIENTIST1)
    waitmovement(OBJECT_SCIENTIST2)
    delay(30)

    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Hey {PLAYER}...!\p"
    "The rock! In your bag!\lIt's shining!"), MSGBOX_DEFAULT)

    callnative(NativePlayerPullOutBall)
    delay(60)
    dofieldeffectsparkle(44, 7, 0)
	waitfieldeffect(FLDEFF_SPARKLE)
    delay(20)
    callnative(NativePlayerPutAwayBall)
    delay(20)
    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("The light from the comet is getting bright!"), MSGBOX_DEFAULT)
    closemessage

    delay(100)
}

movement LunarSummit_Comet_CameraMoveToComet1 { walk_slow_up * 1 }
movement LunarSummit_Comet_CameraMoveToComet2 { walk_slow_up * 2 }

movement LunarSummit_Comet_Scientist1ApproachPlayer { walk_up * 6 }
movement LunarSummit_Comet_Scientist2ApproachPlayer { walk_up * 5 }

movement LunarSummit_Comet_CameraMoveFromComet { walk_slow_down * 3 }
movement LunarSummit_Comet_JirachiMovesToBattle { walk_slow_down * 2 }


script LunarSummit_Jirachi {
    lock
    faceplayer
    waitse
    playmoncry(SPECIES_JIRACHI, CRY_MODE_ENCOUNTER)
    msgbox(format("Wishah wishah!"), MSGBOX_NPC)
    delay(20)
    waitmoncry

    setwildbattle(SPECIES_JIRACHI, 40)
    special(BattleSetup_StartLegendaryBattle)
    waitstate
    specialvar(VAR_RESULT, GetBattleOutcome)
    switch (var(VAR_RESULT)) {
        case B_OUTCOME_CAUGHT:
            setflag(FLAG_CAPTURED_JIRACHI)
            setflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
            call(Common_EventScript_RemoveStaticPokemon)
        case B_OUTCOME_RAN:
            msgbox(format("Jirachi is watching carefully."), MSGBOX_DEFAULT)
        default:
            fadescreenswapbuffers(FADE_TO_BLACK)
            removeobject(OBJECT_JIRACHI)
            fadescreenswapbuffers(FADE_FROM_BLACK)
            msgbox(format("Jirachi fled into a crater..."), MSGBOX_DEFAULT)
    }
    release
    end
}