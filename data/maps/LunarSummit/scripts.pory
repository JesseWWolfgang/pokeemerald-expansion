const VAR_TEMP_WITNESSED_MILLENNIUM_COMET = VAR_TEMP_0
const FLAG_TEMP_SPOKEN_ABOUT_MILLENNIUM_COMET = FLAG_TEMP_1
const OBJECT_JIRACHI = 1
const OBJECT_SCIENTIST1 = 2
const OBJECT_ROCKET = 3
const OBJECT_SCIENTIST2 = 4

const WARP_MOSSDEEP_X = 7
const WARP_MOSSDEEP_Y = 3

const WARP_IN_X = 65
const WARP_IN_Y = 0

const FLIGHT_START_X = 65
const FLIGHT_START_Y = 2

const WISH_CHOICE_MONEY = 0 // Items cost no money in shops.
const WISH_CHOICE_MASTER_BALLS = 1 // Given 20 Master Balls.
const WISH_CHOICE_ITEMS = 2 // Given 99x of a given Item in your Bag.
const WISH_CHOICE_POWER = 3 // Given the option to Min/Max 2 Pokemon's EVs/IVs.
const WISH_CHOICE_REPEL = 4 // Given a permanent repel under Key Items.
const WISH_CHOICE_POKERUS = 5 // Infects your entire team with Pokerus.
const WISH_CHOICE_SHINY = 6 // Turns 1 Pokemon into a Shiny.
const WISH_CHOICE_TEACH_SKETCH = 7 // Teaches the move Sketch to any Pokemon.
const WISH_CHOICE_PERFECT_BIDOOF = 8 // A perfect Bidoof arrives immediately following the battle with Jirachi.

mapscripts LunarSummit_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        if (flag(FLAG_CAUGHT_JIRACHI) || !flag(FLAG_WITNESSED_MILLENNIUM_COMET)) {
            setflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
        }
        else {
            clearflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
            call(LunarSummit_SetScientistsToWatchJirachi)
        }

        if (flag(FLAG_WITNESSED_MILLENNIUM_COMET)) {
            setvar(VAR_TEMP_WITNESSED_MILLENNIUM_COMET, 1)
        }
        else {
            setvar(VAR_TEMP_WITNESSED_MILLENNIUM_COMET, 0)
        }
    }

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_LOADED, FALSE {
            setvar(VAR_TEMP_LOADED, TRUE)
            getplayerxy(VAR_TEMP_2, VAR_TEMP_3)
            if (var(VAR_TEMP_2) == WARP_IN_X && var(VAR_TEMP_3) == WARP_IN_Y) {
                call(LunarSummit_FlightIn)
            }
        }
    ]
}

script LunarSummit_Scientist1 {
    lock
    faceplayer

    // call(LunarSummit_PrepareTakeOff)
    // end


    if (flag(FLAG_WITNESSED_MILLENNIUM_COMET)) {
        if (flag(FLAG_CAUGHT_JIRACHI)) {
            msgbox(format("Ready to leave?"), MSGBOX_YESNO)
            if (var(VAR_RESULT) == YES) {
                msgbox(format("Please board the shuttle and wait for lift off."), MSGBOX_DEFAULT)
                closemessage
                call(LunarSummit_PrepareTakeOff)
            }
        }
        else {
            msgbox(format("We're set up and monitoring. Give it your all {PLAYER}!"), MSGBOX_NPC)
        }
    }
    else {
        msgbox(format("We haven't finished conducting our research here just yet.\p"
        "You could try and find the highest point around here to get a clear view of the comet."), MSGBOX_DEFAULT)
    }
    release
}

script LunarSummit_Scientist2 {
    lock
    faceplayer
    if (flag(FLAG_WITNESSED_MILLENNIUM_COMET)) {
        if (flag(FLAG_CAUGHT_JIRACHI)) {
            msgbox(format("That was really something up there wasn't it?\p"
            "I do hope your wish comes true..."), MSGBOX_NPC)
        }
        else {
            callnative(HasAtLeastOnePokeball)
            if (var(VAR_RESULT) == FALSE) {
                msgbox(format("It seems like you're out of Pokéballs...\p"
                "Here, take these!"), MSGBOX_NPC)
                giveitem(ITEM_MOON_BALL, 20)
            }
        }
    }
    else {
        msgbox(format("Hi {PLAYER}, I'm pretty busy taking sample data at the moment... can't stop for long."), MSGBOX_DEFAULT)
    }
    msgbox(format("Would you like me to restore the health of your Pokémon?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        msgbox(format("Let me take your Pokémon for a moment..."), MSGBOX_DEFAULT)
        fadescreen(FADE_TO_BLACK)
        playfanfare(MUS_HEAL)
        waitfanfare
        special(HealPlayerParty)
        fadescreen(FADE_FROM_BLACK)
        delay(30)
        msgbox(format("Fully healed and ready to go!"), MSGBOX_DEFAULT)
    }
    release
}

script LunarSummit_PrepareTakeOff {
    applymovement(OBJECT_SCIENTIST1, LunarSummit_PrepareTakeOffScientist1MoveForPlayerAndScientist2)
    waitmovement(0)
    applymovementplayerfacing(OBJ_EVENT_ID_PLAYER, 
        LunarSummit_PrepareTakeOffPlayerBoardNorth, 
        LunarSummit_PrepareTakeOffPlayerBoardNorth, 
        LunarSummit_PrepareTakeOffPlayerBoardEast, 
        LunarSummit_PrepareTakeOffPlayerBoardWest)
    applymovement(OBJECT_SCIENTIST2, LunarSummit_PrepareTakeOffScientist2Board)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_LUNAR_SUMMIT)
    playse(SE_EXIT)
    waitmovement(OBJECT_SCIENTIST2)
    hideobjectat(OBJECT_SCIENTIST2, MAP_LUNAR_SUMMIT)
    playse(SE_EXIT)
    applymovement(OBJECT_SCIENTIST1, LunarSummit_PrepareTakeOffScientist1Board)
    waitmovement(0)
    hideobjectat(OBJECT_SCIENTIST1, MAP_LUNAR_SUMMIT)
    playse(SE_EXIT)
    delay(30)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, LunarSummit_PrepareTakeOffCameraToRocket)
    waitmovement(0)
    delay(30)
    
    special(StopMapMusic)
    delay(10)
    playbgm(MUS_EVOLUTION_INTRO, FALSE)
    messageautoscroll(format("T-minus-5...\p4...\p3...\p2...\p1...\pLift off!"))
    waitmessage
    //playbgm(MUS_RG_ENCOUNTER_DEOXYS, FALSE)
    delay(30)
    closemessage

    delay(30)
    call(LunarSummit_RocketTakeOff)
    warpwhitefade(MAP_MOSSDEEP_CITY_SPACE_CENTER_1F, WARP_MOSSDEEP_X, WARP_MOSSDEEP_Y)
	waitstate
    special(RemoveCameraObject)
}

script LunarSummit_RocketTakeOff {
    // Engine Startup
    playse(SE_M_EARTHQUAKE)
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 2)  // horizontal pan
	setvar(VAR_0x8006, 32) // num shakes
	setvar(VAR_0x8007, 2)  // shake delay
	special(ShakeCamera)
    delay(32)

    // Ignition
    applymovement(OBJECT_ROCKET, LunarSummit_TakeOffRocketWarmUp)
    waitmovement(OBJECT_ROCKET)

    // Lift off
    playse(SE_M_EARTHQUAKE)
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 2)  // horizontal pan
	setvar(VAR_0x8006, 50) // num shakes
	setvar(VAR_0x8007, 2)  // shake delay
    special(ShakeCamera)

    // Begin lift off animation
    //special(SpawnCameraObject)
    applymovement(OBJECT_ROCKET, LunarSummit_TakeOffRocketMovement)
    applymovement(OBJ_EVENT_ID_CAMERA, LunarSummit_TakeOffCameraMovement)
    
    // Wait until lift off shake finishes
    waitstate

    // Continue heavy vibrations and speed up
    playse(SE_INTRO_BLAST)
    setvar(VAR_0x8004, 1)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 40) // num shakes
	setvar(VAR_0x8007, 2)  // shake delay
    special(ShakeCamera)
    waitstate

    // Continue light vibrations
    setvar(VAR_0x8004, 1)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 80) // num shakes
	setvar(VAR_0x8007, 1)  // shake delay
    special(ShakeCamera)

    // Wait for speed up sound to complete and play rain sound (since it sounds similar to wind)
    delay(30) 
    playse(SE_RAIN)
    
    // Wait until the animation has finished
    waitmovement(OBJECT_ROCKET)
    waitmovement(OBJ_EVENT_ID_CAMERA)
}

movement LunarSummit_PrepareTakeOffScientist1MoveForPlayerAndScientist2 {
    walk_up * 2
    walk_right
    face_left
}

movement LunarSummit_PrepareTakeOffPlayerBoardNorth { walk_up * 4 }
movement LunarSummit_PrepareTakeOffPlayerBoardEast { walk_right walk_up * 3 }
movement LunarSummit_PrepareTakeOffPlayerBoardWest { walk_left walk_up * 3 }


movement LunarSummit_PrepareTakeOffScientist2Board {
    walk_up * 3
    walk_right * 3
    walk_up * 4
}

movement LunarSummit_PrepareTakeOffScientist1Board {
    walk_left
    walk_up
}

movement LunarSummit_PrepareTakeOffCameraToRocket { walk_up * 2}

movement LunarSummit_TakeOffRocketWarmUp {
    walk_in_place_up * 5
}

movement LunarSummit_TakeOffRocketMovement {
    walk_slow_up * 5
    walk_up * 8
    walk_fast_up * 16
    walk_faster_up * 22
}

movement LunarSummit_TakeOffCameraMovement {
    walk_slow_up * 5
    walk_up * 8
    walk_fast_up * 16
    walk_faster_up * 15
}

script LunarSummit_WitnessMillenniumComet {
    lock
    setflag(FLAG_WITNESSED_MILLENNIUM_COMET)
    setvar(VAR_TEMP_WITNESSED_MILLENNIUM_COMET, 1)
    delay(60)
    special(SpawnCameraObject)

    // Talk to the scientists first
    call(LunarSummit_WitnessMillenniumComet_ScientistsBefore)
    
    // Sparkle
    playse(SE_M_DETECT)
	dofieldeffectsparkle(47, 3, 0)
    dofieldeffectsparkle(46, 3, 0)
    dofieldeffectsparkle(48, 3, 0)
    dofieldeffectsparkle(47, 2, 0)
    dofieldeffectsparkle(47, 4, 0)
	waitfieldeffect(FLDEFF_SPARKLE)
    delay(50)

    // Orb anim
    playfanfare(MUS_AWAKEN_LEGEND)
	playse(SE_ORB)
    setvar(VAR_RESULT, 4)
    special(DoOrbEffect)

    // Sparkle during orb effect (total 500 frames)
    delay(50)
    dofieldeffectsparkle(44, 4, 0)
    delay(50)
    dofieldeffectsparkle(46, 5, 0)
    delay(50)
    dofieldeffectsparkle(43, 1, 0)
    delay(40)
    dofieldeffectsparkle(43, 5, 0)
    delay(40)
    dofieldeffectsparkle(46, 3, 0)
    dofieldeffectsparkle(43, 4, 0)
    delay(40)
    dofieldeffectsparkle(43, 6, 0)
    dofieldeffectsparkle(46, 5, 0)
    delay(30)
    dofieldeffectsparkle(43, 2, 0)
    dofieldeffectsparkle(44, 4, 0)
    delay(30)
    dofieldeffectsparkle(44, 2, 0)
    dofieldeffectsparkle(47, 2, 0)
    dofieldeffectsparkle(44, 5, 0)
    delay(30)
    dofieldeffectsparkle(43, 2, 0)
    dofieldeffectsparkle(44, 4, 0)
    delay(20)
    dofieldeffectsparkle(44, 2, 0)
    dofieldeffectsparkle(47, 2, 0)
    dofieldeffectsparkle(44, 5, 0)
    delay(20)
    dofieldeffectsparkle(43, 2, 0)
    dofieldeffectsparkle(44, 4, 0)
    delay(20)
    dofieldeffectsparkle(44, 1, 0)
    dofieldeffectsparkle(42, 6, 0)
    dofieldeffectsparkle(46, 6, 0)
    delay(20)
    dofieldeffectsparkle(44, 2, 0)
    dofieldeffectsparkle(47, 2, 0)
    dofieldeffectsparkle(44, 5, 0)
    delay(20)
    dofieldeffectsparkle(44, 3, 0)
    dofieldeffectsparkle(44, 5, 0)
    dofieldeffectsparkle(43, 4, 0)
    dofieldeffectsparkle(45, 4, 0)
    delay(20)
    dofieldeffectsparkle(44, 2, 0)
    dofieldeffectsparkle(44, 6, 0)
    dofieldeffectsparkle(42, 4, 0)
    dofieldeffectsparkle(46, 4, 0)
    delay(20)
    dofieldeffectsparkle(44, 3, 0)
    dofieldeffectsparkle(44, 5, 0)
    dofieldeffectsparkle(43, 4, 0)
    dofieldeffectsparkle(45, 4, 0)

    // Fade out the orb and lower the sparkling. (Also fade out the drought weather)
    setweather(WEATHER_NONE)
    doweather
    special(FadeOutOrbEffect)
    delay(10)
    dofieldeffectsparkle(43, 6, 0)
    dofieldeffectsparkle(46, 5, 0)
    delay(10)
    dofieldeffectsparkle(43, 2, 0)
    dofieldeffectsparkle(44, 4, 0)
    delay(20)
    dofieldeffectsparkle(44, 4, 0)
    delay(20)
    dofieldeffectsparkle(46, 5, 0)
    delay(20)
    dofieldeffectsparkle(43, 1, 0)
    delay(20)
    dofieldeffectsparkle(43, 5, 0)
	waitstate

    // Show Jirachi
    fadescreen(FADE_TO_WHITE)
    clearflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
    addobject(OBJECT_JIRACHI)
    setobjectxy(OBJECT_JIRACHI, 44, 4)
    playse(SE_ICE_BREAK) 
    fadescreen(FADE_FROM_WHITE)
    
    // Sparkles when Jirachi appears
    delay(4)
    dofieldeffectsparkle(44, 3, 0)
    delay(4)
    dofieldeffectsparkle(44, 5, 0)
    delay(4)
    dofieldeffectsparkle(43, 4, 0)
    delay(4)
    dofieldeffectsparkle(45, 4, 0)
    delay(4)
    dofieldeffectsparkle(44, 4, 0)

    // Play cry
    waitse
    delay(20)
    playmoncry(SPECIES_JIRACHI, CRY_MODE_ENCOUNTER)
    waitmoncry

    // The scientists are surprised
    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    applymovement(OBJECT_SCIENTIST1, Common_Movement_ExclamationMark)
    applymovement(OBJECT_SCIENTIST2, Common_Movement_ExclamationMark)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    waitse
    delay(80)
    
    // Approach the player
    applymovement(OBJ_EVENT_ID_CAMERA, LunarSummit_Comet_CameraMoveFromComet)
    applymovement(OBJECT_JIRACHI, LunarSummit_Comet_JirachiMovesToBattle)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    waitmovement(OBJECT_JIRACHI)
    special(RemoveCameraObject)
    delay(20)

    // Scientist mentions it wants to play with you
    applymovement(OBJECT_SCIENTIST2, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Is that... a Pokémon?!"), MSGBOX_DEFAULT)
    closemessage
    applymovement(OBJECT_SCIENTIST1, LunarSummit_Comet_ExcitedScientist)
    waitmovement(0)
    msgbox(format("Simply incredible!\p"
    "It's Jirachi!"), MSGBOX_DEFAULT)
    delay(20)
    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlaceFasterUp)
    waitmovement(0)
    msgbox(format("We were right to bring you on this journey {PLAYER}.\p"
    "Jirachi is rumoured to grant a single wish each time it awakens every 1000 years...\p"
    "...and it looks like it may have chosen you..."), MSGBOX_DEFAULT)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    applymovement(OBJECT_SCIENTIST2, Common_Movement_WalkInPlaceFasterUp)
    waitmovement(0)
    msgbox(format("We will observe from the side and collect as much data as we can of this historical moment.\p"
    "Good luck {PLAYER}!"), MSGBOX_DEFAULT)
    closemessage

    // Scientists walk to their sideline positions
    applymovement(OBJECT_SCIENTIST1, LunarSummit_Comet_Scientist1WalkToSideline)
    applymovement(OBJECT_SCIENTIST2, LunarSummit_Comet_Scientist2WalkToSideline)
    waitmovement(OBJECT_SCIENTIST1)
    waitmovement(OBJECT_SCIENTIST2)
    call(LunarSummit_SetScientistsToWatchJirachi)
}

script LunarSummit_SetScientistsToWatchJirachi {
    setobjectxyperm(OBJECT_SCIENTIST1, 46, 10)
    setobjectxyperm(OBJECT_SCIENTIST2, 46, 9)
    setobjectmovementtype(OBJECT_SCIENTIST1, MOVEMENT_TYPE_FACE_LEFT)
    setobjectmovementtype(OBJECT_SCIENTIST2, MOVEMENT_TYPE_FACE_LEFT)
}


script LunarSummit_WitnessMillenniumComet_ScientistsBefore {
    applymovement(OBJ_EVENT_ID_CAMERA, LunarSummit_Comet_CameraMoveToComet1)
    waitmovement(0)
    delay(120)

    movefromoffscreen(OBJECT_SCIENTIST1, 44, 14)
    movefromoffscreen(OBJECT_SCIENTIST2, 45, 13)

    // Scientists approach
    applymovement(OBJECT_SCIENTIST1, LunarSummit_Comet_Scientist1ApproachPlayer)
    applymovement(OBJECT_SCIENTIST2, LunarSummit_Comet_Scientist2ApproachPlayer)
    waitmovement(OBJECT_SCIENTIST1)
    waitmovement(OBJECT_SCIENTIST2)
    delay(30)

    // Scientists mention how beautiful the comet it.
    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Would you look at that..."), MSGBOX_DEFAULT)
    closemessage
    delay(100)
    msgbox(format("This truly is a once in a lifetime view.\p"
        "I used to read books about this sort of thing when I was just a kid.\p"
        "...but seeing something like this first-hand...\lNothing truly prepares you."), MSGBOX_DEFAULT)
    closemessage
    delay(60)
    applymovement(OBJECT_SCIENTIST2, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Look how bright the comet shines. Simply magnificent."), MSGBOX_DEFAULT)
    closemessage
    delay(40)

    // Rock in bag shines
    dofieldeffectsparkle(44, 7, 0)
	waitfieldeffect(FLDEFF_SPARKLE)

    // Scientists are surprised
    playse(SE_PIN)
    applymovement(OBJECT_SCIENTIST1, Common_Movement_ExclamationMark)
    delay(15)
    playse(SE_PIN)
    applymovement(OBJECT_SCIENTIST2, Common_Movement_ExclamationMark)
    waitmovement(OBJECT_SCIENTIST1)
    waitmovement(OBJECT_SCIENTIST2)
    delay(30)
    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Hey {PLAYER}...!\p"
    "The rock! In your bag!\lIt's shining!"), MSGBOX_DEFAULT)
    closemessage

    // Player pulls out the rock as it shines
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
    delay(10)
    callnative(NativePlayerPullOutBall)
    delay(60)
    dofieldeffectsparkle(44, 7, 0)
	waitfieldeffect(FLDEFF_SPARKLE)
    delay(20)
    callnative(NativePlayerPutAwayBall)
    delay(30)
    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Amazing!\nWhat could this possibly mean?"), MSGBOX_DEFAULT)
    closemessage

    // Comet begins to shine brightly
    setweather(WEATHER_DROUGHT)
    doweather
    special(WaitWeather)
    waitstate
    delay(60)
    playse(SE_PIN)
    applymovement(OBJECT_SCIENTIST1, Common_Movement_ExclamationMark)
    waitmovement(OBJECT_SCIENTIST1)
    msgbox(format("Hold on...\p"
    "The light from the comet... it's getting brighter!"), MSGBOX_DEFAULT)
    closemessage

    // Everyone watches the comet
    delay(20)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp)
    waitmovement(0)
    delay(40)
    applymovement(OBJ_EVENT_ID_CAMERA, LunarSummit_Comet_CameraMoveToComet2)
    waitmovement(0)
    delay(60)
}

movement LunarSummit_Comet_CameraMoveToComet1 { walk_slow_up * 1 }
movement LunarSummit_Comet_CameraMoveToComet2 { walk_slow_up * 2 }

movement LunarSummit_Comet_Scientist1ApproachPlayer { walk_up * 6 }
movement LunarSummit_Comet_Scientist2ApproachPlayer { walk_up * 5 }

movement LunarSummit_Comet_CameraMoveFromComet { walk_slow_down * 3 }
movement LunarSummit_Comet_JirachiMovesToBattle { walk_slow_down * 2 }

movement LunarSummit_Comet_ExcitedScientist { walk_in_place_fast_right * 3 }
movement LunarSummit_Comet_Scientist1WalkToSideline { walk_down * 2 walk_right * 2 face_left }
movement LunarSummit_Comet_Scientist2WalkToSideline { walk_down walk_right face_left }


script LunarSummit_Jirachi {
    lock
    if (!flag(FLAG_JIRACHI_WISH_GRANTED)) {
        call(LunarSummit_JirachiGrantWish)
        if (flag(FLAG_JIRACHI_WISH_GRANTED)) { // Only start the battle if a wish was granted.
            call(LunarSummit_JirachiBattle)
        }
    }
    else
    {
        call(LunarSummit_JirachiBattle)
    }

    release
    end
}

script LunarSummit_JirachiBattle {
    objectfaceplayer(OBJECT_JIRACHI)
    waitse
    playmoncry(SPECIES_JIRACHI, CRY_MODE_ENCOUNTER)
    msgbox(format("Wishah wishah!"), MSGBOX_DEFAULT)
    closemessage
    delay(20)
    waitmoncry

    setwildbattle(SPECIES_JIRACHI, 40)
    special(BattleSetup_StartLegendaryBattle)
    waitstate
    specialvar(VAR_RESULT, GetBattleOutcome)
    switch (var(VAR_RESULT)) {
        case B_OUTCOME_CAUGHT:
            setflag(FLAG_CAUGHT_JIRACHI)
            setflag(FLAG_HIDE_SPECIAL_POKEMON_ON_MAP)
            call(Common_EventScript_RemoveStaticPokemon)
        case B_OUTCOME_RAN:
            msgbox(format("Jirachi is watching carefully."), MSGBOX_DEFAULT)
        default:
            fadescreenswapbuffers(FADE_TO_WHITE)
            playse(SE_M_COSMIC_POWER)
            waitse
            playse(SE_SHINY)
            waitse
            fadescreenswapbuffers(FADE_FROM_WHITE)
            msgbox(format("Jirachi was enveloped by a sheen of light, and fully recovered."), MSGBOX_DEFAULT)
            closemessage
    }
}

script LunarSummit_JirachiGrantWish {
    objectfaceplayer(OBJECT_JIRACHI)
    waitse
    playmoncry(SPECIES_JIRACHI, CRY_MODE_ENCOUNTER)
    msgbox(format("Wishah wishah!"), MSGBOX_DEFAULT)
    closemessage
    delay(20)
    waitmoncry
    delay(30)

    // Jirachi does a speeen
    playse(SE_M_HEAL_BELL)
    applymovement(OBJECT_JIRACHI, LunarSummit_JirachiGrantWishSpin)
    waitmovement(0)
    waitse
    delay(60)

    // Jirachi waits for your wish
    msgbox(format("It looks as though Jirachi is waiting to hear your wish."), MSGBOX_DEFAULT)
    while
    {
        msgbox(format("What do you wish for?"), MSGBOX_DEFAULT)

        dynmultipush("Lots of money!", WISH_CHOICE_MONEY)
        dynmultipush("Lots of Master Balls!", WISH_CHOICE_MASTER_BALLS)
        dynmultipush("Make more of an item!", WISH_CHOICE_ITEMS)
        dynmultipush("Maximise a Pokémon's power!", WISH_CHOICE_POWER)
        dynmultipush("Unlimited repel! I hate grass!", WISH_CHOICE_REPEL)
        dynmultipush("Pokérus!", WISH_CHOICE_POKERUS)
        dynmultipush("Make a Pokémon shiny!", WISH_CHOICE_SHINY)
        dynmultipush("Teach me Sketch!", WISH_CHOICE_TEACH_SKETCH)
        dynmultipush("Surprise me!", WISH_CHOICE_PERFECT_BIDOOF)
        dynmultipush("Wait, let me think...", MULTI_B_PRESSED)
        dynmultistack(1, 1, FALSE, 5, FALSE, WISH_CHOICE_MONEY, DYN_MULTICHOICE_CB_NONE)

        if (var(VAR_RESULT) == MULTI_B_PRESSED) {
            setvar(VAR_RESULT, FALSE)
            return
        }

        setvar(VAR_0x8000, 0) // Item / Pokemon party slot
        setvar(VAR_0x8001, 0xFF) // Pokemon Max EV+IV stat 1
        setvar(VAR_0x8002, 0xFF) // Pokemon Max EV+IV stat 2
        setvar(VAR_0x8003, 0) // Pokemon Nature
        copyvar(VAR_TEMP_8, VAR_RESULT) // The result of the dynamic choice
        setvar(VAR_RESULT, NO) // Whether the player is sure of their choice
        switch (var(VAR_TEMP_8)) {
            case WISH_CHOICE_MONEY: msgbox(format("You wish to be able to buy anything you wish? Are you sure?"), MSGBOX_YESNO)
            case WISH_CHOICE_MASTER_BALLS: msgbox(format("You wish to have lots of Master Balls? Are you sure?"), MSGBOX_YESNO)
            case WISH_CHOICE_REPEL: msgbox(format("You wish to be able to have a never-ending supply of repel? Are you sure?"), MSGBOX_YESNO)
            case WISH_CHOICE_POKERUS: msgbox(format("You wish to have your party contract Pokérus? Are you sure?"), MSGBOX_YESNO)
            case WISH_CHOICE_PERFECT_BIDOOF: msgbox(format("A surprise? That seems kind of risky... Are you absolutely sure about this?"), MSGBOX_YESNO)

            case WISH_CHOICE_ITEMS: call(LunarSummit_JirachiGrantWish_Item)
            case WISH_CHOICE_POWER: call(LunarSummit_JirachiGrantWish_Power)
            case WISH_CHOICE_SHINY: call(LunarSummit_JirachiGrantWish_Shiny)
            case WISH_CHOICE_TEACH_SKETCH: call(LunarSummit_JirachiGrantWish_TeachSketch)
        }

        if (var(VAR_RESULT) == YES) {
            break
        }
    }

    setflag(FLAG_JIRACHI_WISH_GRANTED)
    msgbox(format(".............."), MSGBOX_DEFAULT)
    closemessage
    playse(SE_M_HEAL_BELL)
    applymovement(OBJECT_JIRACHI, LunarSummit_JirachiGrantWishSpin)
    waitmovement(0)
    waitse

    // Apply effect
    call(LunarSummit_JirachiApplyWishEffect)

    delay(60)
    playmoncry(SPECIES_JIRACHI, CRY_MODE_ENCOUNTER)
    waitmoncry
    delay(30)
    msgbox(format("You feel your body become light momentarily..."), MSGBOX_DEFAULT)
    closemessage
    delay(60)
}

script LunarSummit_JirachiApplyWishEffect {
    if (var(VAR_TEMP_8) == WISH_CHOICE_MONEY) {
        playse(SE_M_PAY_DAY)
        fadescreenspeed(FADE_TO_WHITE, 1)
        fadescreenspeed(FADE_FROM_WHITE, 1)
        waitse
        addmoney(1000000)
    }

    if (var(VAR_TEMP_8) == WISH_CHOICE_MASTER_BALLS) {
        giveitem(ITEM_MASTER_BALL, 100)
    }

    if (var(VAR_TEMP_8) == WISH_CHOICE_REPEL) {
        giveitem(ITEM_MAX_REPEL, 100)
    }

    if (var(VAR_TEMP_8) == WISH_CHOICE_POKERUS) {
        playse(SE_M_BARRIER)
        fadescreenspeed(FADE_TO_WHITE, 1)
        fadescreenspeed(FADE_FROM_WHITE, 1)
        setmondataforpartymon(0, M_MON_DATA_POKERUS, TRUE)
        setmondataforpartymon(1, M_MON_DATA_POKERUS, TRUE)
        setmondataforpartymon(2, M_MON_DATA_POKERUS, TRUE)
        setmondataforpartymon(3, M_MON_DATA_POKERUS, TRUE)
        setmondataforpartymon(4, M_MON_DATA_POKERUS, TRUE)
        setmondataforpartymon(5, M_MON_DATA_POKERUS, TRUE)
    }

    if (var(VAR_TEMP_8) == WISH_CHOICE_ITEMS) {
        giveitem(VAR_0x8000, 99)
    }

    if (var(VAR_TEMP_8) == WISH_CHOICE_POWER) {
        // Set all IVs max
        setpartymonstat(VAR_0x8000, STAT_HP, 31, TRUE)
        setpartymonstat(VAR_0x8000, STAT_ATK, 31, TRUE)
        setpartymonstat(VAR_0x8000, STAT_DEF, 31, TRUE)
        setpartymonstat(VAR_0x8000, STAT_SPATK, 31, TRUE)
        setpartymonstat(VAR_0x8000, STAT_SPDEF, 31, TRUE)
        setpartymonstat(VAR_0x8000, STAT_SPEED, 31, TRUE)

        // Maximise chosen EVs
        clearpartymonEVs(VAR_0x8000)
        setpartymonstat(VAR_0x8000, VAR_0x8001, 252, FALSE)
        setpartymonstat(VAR_0x8000, VAR_0x8002, 252, FALSE)

        // Set nature
        setpartymonnature(VAR_0x8000, VAR_0x8003)

        recalculatepartymonstats(VAR_0x8000)

        playse(SE_SUPER_EFFECTIVE)
        fadescreenspeed(FADE_TO_WHITE, 1)
        fadescreenspeed(FADE_FROM_WHITE, 1)
        waitse
        showpartymonfieldmove(VAR_0x8000)
    }

    if (var(VAR_TEMP_8) == WISH_CHOICE_SHINY) {
        setmondataforpartymon(VAR_0x8000, M_MON_DATA_IS_SHINY, TRUE)
        playse(SE_SHINY)
        fadescreenspeed(FADE_TO_WHITE, 1)
        fadescreenspeed(FADE_FROM_WHITE, 1)
        waitse
        showpartymonfieldmove(VAR_0x8000)
    }

    if (var(VAR_TEMP_8) == WISH_CHOICE_PERFECT_BIDOOF) {
        setflag(FLAG_JIRACHI_WISH_PERFECT_BIDOOF)
    }
}

script LunarSummit_JirachiGrantWish_Shiny {
    while {
        special(ChoosePartyMon)
        waitstate
        if (var(VAR_0x8004) < PARTY_SIZE) {
            special(IsSelectedMonEgg)
            if (var(VAR_RESULT) == TRUE) {
                msgbox(format("An egg cannot be turned into a shiny."), MSGBOX_DEFAULT)
                closemessage
            }
            else {
                bufferpartymonnick(STR_VAR_1, VAR_0x8004)
                getmondataforpartymon(VAR_0x8004, M_MON_DATA_IS_SHINY)
                if (var(VAR_RESULT) == TRUE) {
                    msgbox(format("{STR_VAR_1} is already shiny."), MSGBOX_DEFAULT)
                    closemessage
                }
                else {
                    copyvar(VAR_0x8000, VAR_0x8004) // Set chosen Pokemon var for later
                    msgbox(format("You want to make your {STR_VAR_1} shiny? This cannot be undone. Are you sure?"), MSGBOX_YESNO)
                    break
                }
            }
        }
        else {
            setvar(VAR_RESULT, NO) // Clean up, we have not decided a wish.
            break
        }
    }
}

script LunarSummit_JirachiGrantWish_Power {
    while {
        setvar(VAR_0x8001, 0xFF) // Pokemon Max EV+IV stat 1
        setvar(VAR_0x8002, 0xFF) // Pokemon Max EV+IV stat 2
        setvar(VAR_0x8003, 0) // Pokemon Nature

        special(ChoosePartyMon)
        waitstate
        if (var(VAR_0x8004) < PARTY_SIZE) {
            special(IsSelectedMonEgg)
            if (var(VAR_RESULT) == TRUE) {
                msgbox(format("An egg cannot have its power improved."), MSGBOX_DEFAULT)
                closemessage
            }
            else {
                bufferpartymonnick(STR_VAR_1, VAR_0x8004)
                copyvar(VAR_0x8000, VAR_0x8004) // Set chosen Pokemon var for later

                msgbox(format("Choose {STR_VAR_1}'s first stat to maximise."), MSGBOX_DEFAULT)
                call(LunarSummit_JirachiGrantWish_Power_StatSelect)
                if (var(VAR_RESULT) != MULTI_B_PRESSED) {
                    closemessage
                    copyvar(VAR_0x8001, VAR_RESULT)
                    msgbox(format("Choose {STR_VAR_1}'s second stat to maximise."), MSGBOX_DEFAULT)
                    call(LunarSummit_JirachiGrantWish_Power_StatSelect)
                    if (var(VAR_RESULT) != MULTI_B_PRESSED) {
                        closemessage
                        copyvar(VAR_0x8002, VAR_RESULT)
                        msgbox(format("Choose {STR_VAR_1}'s new nature."), MSGBOX_DEFAULT)
                        call(LunarSummit_JirachiGrantWish_Power_NatureSelect)
                        if (var(VAR_RESULT) != MULTI_B_PRESSED) {
                            closemessage
                            copyvar(VAR_0x8003, VAR_RESULT)

                            bufferpartymonnick(STR_VAR_1, VAR_0x8000)
                            msgbox(format("You want to perfect your {STR_VAR_1}?"), MSGBOX_DEFAULT)
                            bufferstatname(STR_VAR_1, VAR_0x8001)
                            bufferstatname(STR_VAR_2, VAR_0x8002)
                            buffernaturename(STR_VAR_3, VAR_0x8003)
                            msgbox(format("This will maximise their {STR_VAR_1} and {STR_VAR_2}, and change their nature to {STR_VAR_3}.\p"
                            "This cannot be undone. Are you sure?"), MSGBOX_YESNO)
                            if (var(VAR_RESULT) == YES) {
                                break
                            }
                        }
                    }
                }
            }
        }
        else {
            setvar(VAR_RESULT, NO) // Clean up, we have not decided a wish.
            break
        }
    }
}

script LunarSummit_JirachiGrantWish_Power_StatSelect {
    if (var(VAR_0x8001) != STAT_HP) { dynmultipush(gText_HP3, STAT_HP) }
    if (var(VAR_0x8001) != STAT_ATK) { dynmultipush(gText_Attack, STAT_ATK) }
    if (var(VAR_0x8001) != STAT_DEF) { dynmultipush(gText_Defense, STAT_DEF) }
    if (var(VAR_0x8001) != STAT_SPATK) { dynmultipush(gText_SpAtk, STAT_SPATK) }
    if (var(VAR_0x8001) != STAT_SPDEF) { dynmultipush(gText_SpDef, STAT_SPDEF) }
    if (var(VAR_0x8001) != STAT_SPEED) { dynmultipush(gText_Speed, STAT_SPEED) }
    dynmultistack(1, 1, FALSE, 5, FALSE, STAT_HP, DYN_MULTICHOICE_CB_NONE)
}

script LunarSummit_JirachiGrantWish_Power_NatureSelect {
    setvar(VAR_TEMP_ITERATOR, 0)
    while (var(VAR_TEMP_ITERATOR) < NUM_NATURES) {
        buffernaturename(STR_VAR_1, VAR_TEMP_ITERATOR)
        dynmultipush("{STR_VAR_1}", VAR_TEMP_ITERATOR)
        increment(VAR_TEMP_ITERATOR)
    }
    dynmultistack(1, 1, FALSE, 5, FALSE, 0, DYN_MULTICHOICE_CB_NONE)
}

script LunarSummit_JirachiGrantWish_Item {
    choosebagitem(FALSE, POCKET_ITEMS, FALSE)
    if (var(VAR_ITEM_ID) != ITEM_NONE) {
        bufferitemnameplural(STR_VAR_1, VAR_ITEM_ID, 2)
        msgbox(format("You wish to make more {STR_VAR_1}? Are you sure?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
            copyvar(VAR_0x8000, VAR_ITEM_ID)
        }
    }
    else {
        setvar(VAR_RESULT, NO) // Clean up, we have not decided a wish.
    }
}

script LunarSummit_JirachiGrantWish_TeachSketch {
    fadescreen(FADE_TO_BLACK)
    setflag(FLAG_ANY_MOVE_TEACHABLE)
    setvar(VAR_0x8005, MOVE_SKETCH)
    special(ChooseMonForMoveTutor)
    waitstate
    clearflag(FLAG_ANY_MOVE_TEACHABLE)
}


movement LunarSummit_JirachiGrantWishSpin {
    face_left
    delay_16
    face_up
    delay_16
    face_right
    delay_16
    face_down
}

script LunarSummit_FlightIn {
    lock
    hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_LUNAR_SUMMIT)

    // Hide these guys
    setobjectxyperm(OBJECT_SCIENTIST1, 0, 0)
    setobjectxyperm(OBJECT_SCIENTIST2, 0, 0)

    movefromoffscreen(OBJECT_ROCKET, FLIGHT_START_X, FLIGHT_START_Y)
    callnative(FillPalBufferBlack) // Required to not flash the rocket before fading out since movefromoffscreen/addobject also loads its palette.

    // Fast landing
    playse(SE_DOWNPOUR)
    applymovement(OBJECT_ROCKET, LunarSummit_FlightIn_DownFast)
    applymovement(OBJ_EVENT_ID_PLAYER, LunarSummit_FlightIn_DownFast)
    fadescreenspeed(FADE_FROM_BLACK, 18)
    waitmovement(OBJECT_ROCKET)
    waitmovement(OBJ_EVENT_ID_PLAYER)

    // Slow landing
    playse(SE_DOWNPOUR_STOP)
    applymovement(OBJECT_ROCKET, LunarSummit_FlightIn_DownSlow)
    applymovement(OBJ_EVENT_ID_PLAYER, LunarSummit_FlightIn_DownSlow)
    waitmovement(OBJECT_ROCKET)
    waitmovement(OBJ_EVENT_ID_PLAYER)

    // Hit ground
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 2)  // horizontal pan
	setvar(VAR_0x8006, 3) // num shakes
	setvar(VAR_0x8007, 4)  // shake delay
    special(ShakeCamera)
    playse(SE_LAVARIDGE_FALL_WARP)
    waitse
    delay(90)

    // Success!
    playfanfare(MUS_EVOLVED)
    messageautoscroll(format("Successful landing number 101!"))
    waitmessage
    waitfanfare
    delay(40)
    closemessage

    // Move camera down to player pos
    applymovement(OBJ_EVENT_ID_PLAYER, LunarSummit_FlightIn_CameraToPlayer)
    waitmovement(0)

    // Scientists exit
    movefromoffscreen(OBJECT_SCIENTIST1, 65, 58)
    applymovement(OBJECT_SCIENTIST1, LunarSummit_FlightIn_Scientist1Exit)
    playse(SE_TRUCK_DOOR)
    waitse
    waitmovement(0)

    movefromoffscreen(OBJECT_SCIENTIST2, 65, 58)
    applymovement(OBJECT_SCIENTIST2, LunarSummit_FlightIn_Scientist2Exit)
    waitmovement(0)

    // Player exits
    showobjectat(OBJ_EVENT_ID_PLAYER, MAP_LUNAR_SUMMIT)
    applymovement(OBJ_EVENT_ID_PLAYER, LunarSummit_FlightIn_PlayerExit)
    waitmovement(0)

    // Talk to scientist 1
    playerfaceobject(OBJECT_SCIENTIST1)
    delay(20)
    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Here we are {PLAYER}.\nPlease, after you..."), MSGBOX_DEFAULT)
    closemessage

    // Walk to intended positions
    applymovement(OBJ_EVENT_ID_PLAYER, LunarSummit_Landed_PlayerWalkIn)
    delay(30)
    applymovement(OBJECT_SCIENTIST2, LunarSummit_Landed_Scientist2WalkIn)
    delay(30)
    applymovement(OBJECT_SCIENTIST1, LunarSummit_Landed_Scientist1WalkIn)
    delay(30)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    waitmovement(OBJECT_SCIENTIST1)

    // Talk to scientist again
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    delay(30)
    msgbox(format("We are going to spend some time setting up and collecting data for the mission.\p"
    "Why don't you take a bit of a moonwalk in the meantime?\p"
    "See if you can get to the highest point around here. It should be the best place to view the Millennium Comet!\p"
    "Can we depend on you for this {PLAYER}?"), MSGBOX_YESNO)
    
    //msgbox(format("Can we depend on you for this {PLAYER}?"), MSGBOX_YESNO)

    if (var(VAR_RESULT) == YES) {
        msgbox(format("I knew we could! We will catch up to you soon okay?\p"
        "If you ever need to restore your team's health, come see us. Good luck!"), MSGBOX_DEFAULT)
    } 
    else {
        msgbox(format("It's okay to be nervous. Don't worry, we have your back.\p"
        "If you ever need to restore your team's health, come see us. Good luck!"), MSGBOX_DEFAULT)
    }
    closemessage
    waitmovement(OBJECT_SCIENTIST2)

    // Set objects to their final positions.
    fadescreen(FADE_TO_BLACK)
    setobjectxyperm(OBJECT_ROCKET, 65, 58)
    setobjectxyperm(OBJECT_SCIENTIST1, 63, 61)
    setobjectxyperm(OBJECT_SCIENTIST2, 61, 65)
    setobjectxy(OBJECT_SCIENTIST1, 63, 61)
    setobjectxy(OBJECT_SCIENTIST2, 61, 65)
    turnobject(OBJECT_SCIENTIST1, DIR_SOUTH)
    turnobject(OBJECT_SCIENTIST2, DIR_SOUTH)
    fadescreen(FADE_FROM_BLACK)

    release
}

movement LunarSummit_FlightIn_DownFast {
    walk_faster_down * 10
    walk_fast_down * 20
    walk_down * 18
}

movement LunarSummit_FlightIn_DownSlow {
    walk_slow_down * 8
}

movement LunarSummit_FlightIn_CameraToPlayer {
    walk_down * 2
}

movement LunarSummit_FlightIn_Scientist1Exit {
    walk_slow_down
    walk_left
    face_right
}

movement LunarSummit_FlightIn_Scientist2Exit {
    walk_slow_down
    walk_right
    face_left
}

movement LunarSummit_FlightIn_PlayerExit {
    walk_slow_down
}

movement LunarSummit_Landed_PlayerWalkIn {
    walk_down * 3
}

movement LunarSummit_Landed_Scientist1WalkIn {
    walk_right
    walk_down * 2
}

movement LunarSummit_Landed_Scientist2WalkIn {
    walk_left
    walk_down * 2
    walk_left * 4
    walk_down * 4
}