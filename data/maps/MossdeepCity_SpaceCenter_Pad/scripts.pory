const OBJECT_BIRCH = 11
const OBJECT_MOM = 12
const OBJECT_SCIENTIST1 = 4
const OBJECT_SCIENTIST2 = 3
const OBJECT_GUIDE_LADY = 13
const OBJECT_ROCKET = 1
const OBJECT_BOAT = 14
const OBJECT_STEVEN = 15

const LANDING_START_X = 28
const LANDING_START_Y = 2

const WARP_BACK_OF_BUILDING_X = 19
const WARP_BACK_OF_BUILDING_Y = 43
const WARP_LANDING_IN_X = 28
const WARP_LANDING_IN_Y = 0
const WARP_MOSSDEEP_SPACE_CENTER_X = 7
const WARP_MOSSDEEP_SPACE_CENTER_Y = 3

const FLAG_HIDE_BIRCH = FLAG_TEMP_1
const FLAG_HIDE_BOAT = FLAG_TEMP_2
const FLAG_HIDE_STEVEN = FLAG_TEMP_3


mapscripts MossdeepCity_SpaceCenter_Pad_MapScripts {
    MAP_SCRIPT_ON_LOAD {
        setflag(FLAG_HIDE_BIRCH)
        setflag(FLAG_HIDE_BOAT)
        setflag(FLAG_HIDE_STEVEN)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_LOADED, FALSE {
            setvar(VAR_TEMP_LOADED, TRUE)
            getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
            if (var(VAR_TEMP_0) == WARP_BACK_OF_BUILDING_X && var(VAR_TEMP_1) == WARP_BACK_OF_BUILDING_Y) {
                call(MossdeepCity_SpaceCenter_Pad_GuideLady_Entrance)
            }
            elif (var(VAR_TEMP_0) == WARP_LANDING_IN_X && var(VAR_TEMP_1) == WARP_LANDING_IN_Y) {
                call(MossdeepCity_SpaceCenter_Pad_LandingIn)
            }
        }
    ]
}

script TEST_Warp_to_Castelia {
    warp(MAP_CASTELIA_CITY_LIBERTY_PIER, 0)
    waitstate
}

script MossdeepCity_SpaceCenter_Pad_Sign { 
    fadescreen(FADE_TO_BLACK)
    preventwarpfadeout
    preventwarpfadein
    warp(MAP_LUNAR_SUMMIT, 0)
	waitstate
    // msgbox(format("Mossdeep Space Center Launch Pad"), MSGBOX_SIGN) 
}

script MossdeepCity_SpaceCenter_Pad_GuideLady {
    lock
    faceplayer
    msgbox(format("Are you not prepared? Would you like to return inside?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        msgbox(format("Okay, but please don't dawdle, you are due to leave imminently."), MSGBOX_DEFAULT)
        warp(MAP_MOSSDEEP_CITY_SPACE_CENTER_1F, WARP_MOSSDEEP_SPACE_CENTER_X, WARP_MOSSDEEP_SPACE_CENTER_Y)
	    waitstate
    }
    release
}

script MossdeepCity_SpaceCenter_Pad_GuideLady_Entrance {
    lock
    objectfaceplayer(OBJECT_GUIDE_LADY)
    msgbox(format("Here is the launch pad for the Mossdeep Space Shuttle. Your crew are up ahead.\p"
    "I wish you the best of luck!"), MSGBOX_DEFAULT)
    release
}

script MossdeepCity_SpaceCenter_Pad_Scientist2 {
    msgbox(format("How are you feeling {PLAYER}? I'm excited to see what this journey holds in store for us."), MSGBOX_NPC)
    applymovement(OBJECT_SCIENTIST2, Common_Movement_FaceDown)
    waitmovement(0)
}

script MossdeepCity_SpaceCenter_Pad_Scientist1 {
    lock
    faceplayer
    msgbox(format("Here we are {PLAYER}!\p"
    "You're absolutely sure you're all set to leave?"), MSGBOX_YESNO)
    closemessage
    if (var(VAR_RESULT) == NO) {
        msgbox(format("Take a brief moment to collect yourself, we will be waiting here."), MSGBOX_DEFAULT)
        applymovement(OBJECT_SCIENTIST1, Common_Movement_FaceDown)
        waitmovement(0)
        closemessage
        release
        end
    }

    if (var(VAR_FACING) == DIR_WEST) {
        applymovement(OBJ_EVENT_ID_PLAYER, MossdeepCity_SpaceCenter_Pad_MoveInFrontOfScientist)
        waitmovement(0)
        applymovement(OBJECT_SCIENTIST1, Common_Movement_FaceDown)
        waitmovement(0)
    }
    msgbox(format("Our ground team has already briefed you and your Pok√©mon on our spaceship travel protocol.\p"
    "You will be responsible for them during the journey.\p"
    "I'm also sure they will do their absolute best for you, should you need them."), MSGBOX_DEFAULT)
    closemessage
    applymovement(OBJECT_SCIENTIST2, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Hey {PLAYER}, looks like there are some people here to see you..."), MSGBOX_DEFAULT)
    closemessage
    delay(20)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
    waitmovement(0)
    delay(20)

    // Spawn birch and mom
    clearflag(FLAG_HIDE_BIRCH)
    addobject(OBJECT_BIRCH, MAP_MOSSDEEP_CITY_SPACE_CENTER_PAD)
    addobject(OBJECT_MOM, MAP_MOSSDEEP_CITY_SPACE_CENTER_PAD)
    applymovement(OBJECT_BIRCH, MossdeepCity_SpaceCenter_Pad_BirchMoveToPlayer)
    applymovement(OBJECT_MOM, MossdeepCity_SpaceCenter_Pad_MomMoveToPlayer)
    waitmovement(OBJECT_BIRCH)
    waitmovement(OBJECT_MOM)

    // Birch talks
    applymovement(OBJECT_BIRCH, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Would you look at this. I'd never have imagined our own {PLAYER} being invited to the moon!"), MSGBOX_DEFAULT)
    closemessage

    // Scientists talks
    applymovement(OBJECT_SCIENTIST2, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Well if it isn't Professor Birch.\p"
    "Pleasure to see you sir."), MSGBOX_DEFAULT)
    closemessage

    // Mom talks
    applymovement(OBJECT_MOM, Common_Movement_WalkInPlace)
    waitmovement(0)
    playerfaceobject(OBJECT_MOM)
    msgbox(format("You didn't think you were just gonna go up and leave to space without saying goodbye did you?\p"
    "I'm so proud of you {PLAYER}! Your father also sends his best wishes too. Go get em!"), MSGBOX_DEFAULT)
    closemessage

    // Birch talks
    applymovement(OBJECT_BIRCH, Common_Movement_WalkInPlace)
    waitmovement(0)
    playerfaceobject(OBJECT_BIRCH)
    msgbox(format("I wish I got the chance to come with you.\p"
    "Give it your all and good luck with your mission!\p"
    "We will be watching the news coverage and waiting for you back down here at home!\p"
    "I hope you uncover the mystery of the Millennium Comet!"), MSGBOX_DEFAULT)
    closemessage

    // Scientists 2 talks
    applymovement(OBJECT_SCIENTIST2, Common_Movement_WalkInPlace)
    waitmovement(0)
    playerfaceobject(OBJECT_SCIENTIST2)
    msgbox(format("I'm sure there will be a next time Professor. You're always welcome."), MSGBOX_DEFAULT)
    closemessage

    // Scientists 1 talks
    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlace)
    waitmovement(0)
    playerfaceobject(OBJECT_SCIENTIST1)
    msgbox(format("Alright {PLAYER}.\p"
    "It's time to board the space shuttle.\nPlease follow us."), MSGBOX_DEFAULT)
    closemessage

    call(MossdeepCity_SpaceCenter_Pad_PrepareTakeOff)
}

movement MossdeepCity_SpaceCenter_Pad_MoveInFrontOfScientist {
    walk_down
    walk_left
    face_up
}

movement MossdeepCity_SpaceCenter_Pad_BirchMoveToPlayer {
    walk_up * 5
}

movement MossdeepCity_SpaceCenter_Pad_MomMoveToPlayer {
    walk_up * 6
    face_left
}

movement MossdeepCity_SpaceCenter_Pad_Scientist1BoardRocket {
    walk_slow_up * 2
}

movement MossdeepCity_SpaceCenter_Pad_Scientist2BoardRocket {
    walk_right
    walk_slow_up * 2
}

movement MossdeepCity_SpaceCenter_Pad_PlayerBoardRocket {
    walk_up
    walk_slow_up * 2
}

script MossdeepCity_SpaceCenter_Pad_PrepareTakeOff {
    applymovement(OBJECT_SCIENTIST1, MossdeepCity_SpaceCenter_Pad_Scientist1BoardRocket)
    waitmovement(0)
    hideobjectat(OBJECT_SCIENTIST1, MAP_MOSSDEEP_CITY_SPACE_CENTER_PAD)
    playse(SE_EXIT)

    applymovement(OBJECT_SCIENTIST2, MossdeepCity_SpaceCenter_Pad_Scientist2BoardRocket)
    waitmovement(0)
    hideobjectat(OBJECT_SCIENTIST2, MAP_MOSSDEEP_CITY_SPACE_CENTER_PAD)
    playse(SE_EXIT)

    applymovement(OBJECT_MOM, Common_Movement_FaceUp)
    applymovement(OBJ_EVENT_ID_PLAYER, MossdeepCity_SpaceCenter_Pad_PlayerBoardRocket)

    messageautoscroll(format("Good luck and farewell {PLAYER}!"))
    waitmovement(OBJ_EVENT_ID_PLAYER)
    hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_MOSSDEEP_CITY_SPACE_CENTER_PAD)
    playse(SE_EXIT)
    waitmessage
    delay(40)
    closemessage

    delay(30)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, MossdeepCity_SpaceCenter_Pad_PrepareTakeOffCameraToRocket)
    waitmovement(0)
    delay(30)

    special(StopMapMusic)
    delay(10)
    playbgm(MUS_EVOLUTION_INTRO, FALSE)

    messageautoscroll(format("T-minus-5...\p4...\p3...\p2...\p1...\pLift off!"))
    waitmessage
    playbgm(MUS_RG_HALL_OF_FAME, FALSE)
    delay(60)
    closemessage

    delay(30)
    call(MossdeepCity_SpaceCenter_Pad_RocketTakeOff)
    preventwarpfadeout
    preventwarpfadein
    warp(MAP_LUNAR_SUMMIT, 0)
	waitstate
    // special(RemoveCameraObject)
}

script MossdeepCity_SpaceCenter_Pad_RocketTakeOff {
    // Engine Startup
    playse(SE_M_EARTHQUAKE)
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 2)  // horizontal pan
	setvar(VAR_0x8006, 32) // num shakes
	setvar(VAR_0x8007, 2)  // shake delay
	special(ShakeCamera)
    delay(32)

    // Ignition
    applymovement(OBJECT_ROCKET, MossdeepCity_SpaceCenter_Pad_TakeOffRocketWarmUp)
    waitmovement(OBJECT_ROCKET)

    // Lift off
    playse(SE_M_EARTHQUAKE)
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 2)  // horizontal pan
	setvar(VAR_0x8006, 50) // num shakes
	setvar(VAR_0x8007, 2)  // shake delay
    special(ShakeCamera)

    // Begin lift off animation
    //special(SpawnCameraObject)
    applymovement(OBJECT_ROCKET, MossdeepCity_SpaceCenter_Pad_TakeOffRocketMovement)
    applymovement(OBJ_EVENT_ID_CAMERA, MossdeepCity_SpaceCenter_Pad_TakeOffCameraMovement)
    
    // Wait until lift off shake finishes
    waitstate

    // Continue heavy vibrations and speed up
    playse(SE_INTRO_BLAST)
    setvar(VAR_0x8004, 1)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 40) // num shakes
	setvar(VAR_0x8007, 2)  // shake delay
    special(ShakeCamera)
    waitstate

    // Continue light vibrations
    setvar(VAR_0x8004, 1)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 80) // num shakes
	setvar(VAR_0x8007, 1)  // shake delay
    special(ShakeCamera)

    // Wait for speed up sound to complete and play rain sound (since it sounds similar to wind)
    delay(30) 
    playse(SE_RAIN)
    
    // Wait until the slower movement completes and go fast
    waitmovement(OBJECT_ROCKET)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    applymovement(OBJECT_ROCKET, MossdeepCity_SpaceCenter_Pad_TakeOffRocketFast)
    applymovement(OBJ_EVENT_ID_CAMERA, MossdeepCity_SpaceCenter_Pad_TakeOffRocketFast)

    // Sonic boom 1
    fadescreen(FADE_TO_WHITE)
    playse(SE_M_SKY_UPPERCUT)
    fadescreen(FADE_FROM_WHITE)

    // Keep going fast
    waitmovement(OBJECT_ROCKET)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    applymovement(OBJECT_ROCKET, MossdeepCity_SpaceCenter_Pad_TakeOffRocketFast)
    applymovement(OBJ_EVENT_ID_CAMERA, MossdeepCity_SpaceCenter_Pad_TakeOffRocketFast)

    // Sonic boom 2
    fadescreen(FADE_TO_WHITE)
    playse(SE_M_SKY_UPPERCUT)
    fadescreen(FADE_FROM_WHITE)

    // More fast
    waitmovement(OBJECT_ROCKET)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    applymovement(OBJECT_ROCKET, MossdeepCity_SpaceCenter_Pad_TakeOffRocketExit)
    applymovement(OBJ_EVENT_ID_CAMERA, MossdeepCity_SpaceCenter_Pad_TakeOffRocketFast)

    // Exit atmosphere
    playse(SE_INTRO_BLAST)
    fadescreenspeed(FADE_TO_BLACK, 18)

    // Wait until the last fast section finishes
    waitmovement(OBJECT_ROCKET)
    waitmovement(OBJ_EVENT_ID_CAMERA)

    playse(SE_RAIN_STOP)
    waitse
    playse(SE_EXIT)
    waitse
    delay(100)
    fadeoutbgm(0)
    delay(60)
    special(StopMapMusic)
    msgbox(format("After a number of long days in space flight..."), MSGBOX_DEFAULT)
    closemessage
}

movement MossdeepCity_SpaceCenter_Pad_PrepareTakeOffCameraToRocket { walk_up * 2 }

movement MossdeepCity_SpaceCenter_Pad_TakeOffRocketWarmUp {
    walk_in_place_up * 5
}

movement MossdeepCity_SpaceCenter_Pad_TakeOffRocketMovement {
    walk_slow_up * 5
    walk_up * 8
    walk_fast_up * 16
}

movement MossdeepCity_SpaceCenter_Pad_TakeOffRocketFast {
    walk_faster_up * 30
}

movement MossdeepCity_SpaceCenter_Pad_TakeOffRocketExit {
    walk_faster_up * 40
}

movement MossdeepCity_SpaceCenter_Pad_TakeOffCameraMovement {
    walk_slow_up * 5
    walk_up * 8
    walk_fast_up * 16
}

// lands at 28, 39
script MossdeepCity_SpaceCenter_Pad_LandingIn {
    clearflag(FLAG_MAP_MUSIC_OVERRIDE)

    lock
    hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_MOSSDEEP_CITY_SPACE_CENTER_PAD)

    // Hide these guys
    setobjectxyperm(OBJECT_SCIENTIST1, 0, 0)
    setobjectxyperm(OBJECT_SCIENTIST2, 0, 0)

    movefromoffscreen(OBJECT_ROCKET, LANDING_START_X, LANDING_START_Y)
    callnative(FillPalBufferWhite) // Required to not flash the rocket before fading out since movefromoffscreen/addobject also loads its palette.

    // Fast landing
    playse(SE_DOWNPOUR)
    applymovement(OBJECT_ROCKET, MossdeepCity_SpaceCenter_Pad_LandingIn_Down)
    applymovement(OBJ_EVENT_ID_PLAYER, MossdeepCity_SpaceCenter_Pad_LandingIn_Down)
    fadescreenspeed(FADE_FROM_WHITE, 18)
    waitmovement(OBJECT_ROCKET)
    waitmovement(OBJ_EVENT_ID_PLAYER)

    // Slow landing
    playse(SE_DOWNPOUR_STOP)
    applymovement(OBJECT_ROCKET, MossdeepCity_SpaceCenter_Pad_LandingIn_DownSlow)
    applymovement(OBJ_EVENT_ID_PLAYER, MossdeepCity_SpaceCenter_Pad_LandingIn_DownSlow)
    waitmovement(OBJECT_ROCKET)
    waitmovement(OBJ_EVENT_ID_PLAYER)

    // Hit ground
    setvar(VAR_0x8004, 0)  // vertical pan
	setvar(VAR_0x8005, 2)  // horizontal pan
	setvar(VAR_0x8006, 3) // num shakes
	setvar(VAR_0x8007, 4)  // shake delay
    special(ShakeCamera)
    playse(SE_LAVARIDGE_FALL_WARP)
    waitse
    delay(90)

    // Success!
    playfanfare(MUS_EVOLVED)
    messageautoscroll(format("Successfully landed home safely!"))
    waitmessage
    waitfanfare
    delay(40)
    closemessage

    // Move camera down to player pos
    applymovement(OBJ_EVENT_ID_PLAYER, MossdeepCity_SpaceCenter_Pad_LandingIn_CameraToPlayer)
    waitmovement(0)

    // Scientists exit
    movefromoffscreen(OBJECT_SCIENTIST1, 28, 39)
    applymovement(OBJECT_SCIENTIST1, MossdeepCity_SpaceCenter_Pad_LandingIn_Scientist1Exit)
    playse(SE_TRUCK_DOOR)
    waitse
    waitmovement(0)

    movefromoffscreen(OBJECT_SCIENTIST2, 28, 39)
    applymovement(OBJECT_SCIENTIST2, MossdeepCity_SpaceCenter_Pad_LandingIn_Scientist2Exit)
    waitmovement(0)

    // Player exits
    showobjectat(OBJ_EVENT_ID_PLAYER, MAP_MOSSDEEP_CITY_SPACE_CENTER_PAD)
    applymovement(OBJ_EVENT_ID_PLAYER, MossdeepCity_SpaceCenter_Pad_LandingIn_PlayerExit)
    waitmovement(0)

    // Talk to scientist 1
    playerfaceobject(OBJECT_SCIENTIST1)
    delay(20)
    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("We've returned in one piece {PLAYER}!\p"
    "The landing party should be here any moment to take us back."), MSGBOX_DEFAULT)
    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlaceLeft)
    msgbox(format("In fact.\p"
    "Here they are now!"), MSGBOX_DEFAULT)
    closemessage
    delay(30)
    playse(SE_RG_SS_ANNE_HORN)
    waitse
    delay(30)

    // Player is as 28, 40
    special(Overworld_PlaySpecialMapMusic)

    delay(20)
    clearflag(FLAG_HIDE_BOAT)
    addobject(OBJECT_BOAT, MAP_MOSSDEEP_CITY_SPACE_CENTER_PAD)
    applymovement(OBJECT_BOAT, MossdeepCity_SpaceCenter_Pad_BoatRide_BoatEnter)

    applymovement(OBJ_EVENT_ID_PLAYER, MossdeepCity_SpaceCenter_Pad_BoatRide_PlayerToBoat)
    delay(30)
    applymovement(OBJECT_SCIENTIST1, MossdeepCity_SpaceCenter_Pad_BoatRide_Scientist1ToBoat)
    applymovement(OBJECT_SCIENTIST2, MossdeepCity_SpaceCenter_Pad_BoatRide_Scientist2ToBoat)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    waitmovement(OBJECT_SCIENTIST1)
    waitmovement(OBJECT_SCIENTIST2)
    delay(60)

    clearflag(FLAG_HIDE_STEVEN)
    addobject(OBJECT_STEVEN, MAP_MOSSDEEP_CITY_SPACE_CENTER_PAD)
    applymovement(OBJECT_STEVEN, Common_Movement_WalkRight)
    waitmovement(0)
    delay(30)

    msgbox(format("Welcome home {PLAYER}. Enjoy your trip?"), MSGBOX_DEFAULT)
    closemessage

    applymovement(OBJECT_SCIENTIST1, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Mr. Stone himself! A pleasure to see you again."), MSGBOX_DEFAULT)
    closemessage

    applymovement(OBJECT_STEVEN, Common_Movement_WalkInPlace)
    waitmovement(0)
    msgbox(format("Hope you had a safe journey back.\p"
    "Let's not dwardle, and get you back to the space center.\p"
    "All aboard!"), MSGBOX_DEFAULT)
    closemessage

    applymovement(OBJECT_STEVEN, MossdeepCity_SpaceCenter_Pad_BoatRide_StevenBoard)
    applymovement(OBJ_EVENT_ID_PLAYER, MossdeepCity_SpaceCenter_Pad_BoatRide_PlayerBoard)
    applymovement(OBJECT_SCIENTIST1, MossdeepCity_SpaceCenter_Pad_BoatRide_Scientist1Board)
    applymovement(OBJECT_SCIENTIST2, MossdeepCity_SpaceCenter_Pad_BoatRide_Scientist2Board)
    waitmovement(OBJECT_STEVEN)
    removeobject(OBJECT_STEVEN)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_MOSSDEEP_CITY_SPACE_CENTER_PAD)
    playse(SE_EXIT)
    waitmovement(OBJECT_SCIENTIST1)
    removeobject(OBJECT_SCIENTIST1)
    waitmovement(OBJECT_SCIENTIST2)
    removeobject(OBJECT_SCIENTIST2)

    delay(60)
    applymovement(OBJECT_BOAT, MossdeepCity_SpaceCenter_Pad_BoatRide_BoatToSpaceCenter)
    applymovement(OBJ_EVENT_ID_PLAYER, MossdeepCity_SpaceCenter_Pad_BoatRide_BoatToSpaceCenter)
    waitmovement(0)

    warp(MAP_MOSSDEEP_CITY_SPACE_CENTER_1F, WARP_MOSSDEEP_SPACE_CENTER_X, WARP_MOSSDEEP_SPACE_CENTER_Y)
    waitstate

    // Set objects to their final positions.
    // fadescreen(FADE_TO_BLACK)
    // setobjectxyperm(OBJECT_ROCKET, 65, 58)
    // setobjectxyperm(OBJECT_SCIENTIST1, 63, 61)
    // setobjectxyperm(OBJECT_SCIENTIST2, 61, 65)
    // setobjectxy(OBJECT_SCIENTIST1, 63, 61)
    // setobjectxy(OBJECT_SCIENTIST2, 61, 65)
    // turnobject(OBJECT_SCIENTIST1, DIR_SOUTH)
    // turnobject(OBJECT_SCIENTIST2, DIR_SOUTH)
    // fadescreen(FADE_FROM_BLACK)

    release
}

movement MossdeepCity_SpaceCenter_Pad_LandingIn_Down {
    walk_fast_down * 10
    walk_down * 18
}

movement MossdeepCity_SpaceCenter_Pad_LandingIn_DownSlow {
    walk_slow_down * 9
}

movement MossdeepCity_SpaceCenter_Pad_LandingIn_CameraToPlayer {
    walk_down * 2
}

movement MossdeepCity_SpaceCenter_Pad_LandingIn_Scientist1Exit {
    walk_slow_down
    walk_left
    face_right
}

movement MossdeepCity_SpaceCenter_Pad_LandingIn_Scientist2Exit {
    walk_slow_down
    walk_right
    face_left
}

movement MossdeepCity_SpaceCenter_Pad_LandingIn_PlayerExit {
    walk_slow_down
}

movement MossdeepCity_SpaceCenter_Pad_BoatRide_BoatEnter {
    walk_slow_right * 3
}

movement MossdeepCity_SpaceCenter_Pad_BoatRide_PlayerToBoat {
    walk_down
    walk_left * 4
}

movement MossdeepCity_SpaceCenter_Pad_BoatRide_Scientist1ToBoat {
    walk_left * 3
}

movement MossdeepCity_SpaceCenter_Pad_BoatRide_Scientist2ToBoat {
    walk_left * 4
}

movement MossdeepCity_SpaceCenter_Pad_BoatRide_StevenBoard {
    walk_left
}

movement MossdeepCity_SpaceCenter_Pad_BoatRide_PlayerBoard {
    walk_left * 3
}

movement MossdeepCity_SpaceCenter_Pad_BoatRide_Scientist1Board {
    walk_down
    walk_left
}

movement MossdeepCity_SpaceCenter_Pad_BoatRide_Scientist2Board {
    walk_down
    walk_left * 2
}

movement MossdeepCity_SpaceCenter_Pad_BoatRide_BoatToSpaceCenter {
    walk_slow_left * 2
    walk_slow_down * 2
    walk_down * 4
    walk_fast_down * 18
    walk_down * 4
    walk_slow_down * 2
}