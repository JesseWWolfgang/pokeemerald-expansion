const OBJECT_SSTIDAL = 1
const OBJECT_SAILOR = 2

const WAREHOUSE_DOOR_X = 72
const WAREHOUSE_DOOR_Y = 7
const SAILOR_WARP_X = 52
const SAILOR_WARP_Y = 58
const LILYCOVE_WARP_X = 8
const LILYCOVE_WARP_Y = 11

mapscripts Unova_Route17_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_LOADED, 0: Unova_Route17_SailorThisIsAsFarAsIGo
    ]
    MAP_SCRIPT_ON_TRANSITION {
        getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
        if (var(VAR_TEMP_0) == WAREHOUSE_DOOR_X && var(VAR_TEMP_1) == WAREHOUSE_DOOR_Y) {
            setweather(WEATHER_RAIN_THUNDERSTORM)
        }
    }
}

script Unova_Route17_RouteSign { msgbox(format("{LEFT_ARROW} Route 17\n  Route 1 {RIGHT_ARROW}"), MSGBOX_SIGN) }
script Unova_Route17_TeamPlasmaBlockedSign { msgbox(format("This route gate has been closed off for your protection.\l- Team Plasma"), MSGBOX_SIGN) }

script Unova_Route17_SailorThisIsAsFarAsIGo {
    setvar(VAR_TEMP_LOADED, 1)
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if (var(VAR_TEMP_0) == SAILOR_WARP_X && var(VAR_TEMP_1) == SAILOR_WARP_Y) {
        lock
        delay(20)
        applymovement(OBJECT_SAILOR, Common_Movement_FacePlayer)
        waitmovement(0)
        msgbox(format("This is about as far as I can take you. You'll have to surf the rest of the way. Good luck."), MSGBOX_DEFAULT)
        release
    }
}

script Unova_Route17_SailorInteract {
    lock
    faceplayer
    getweather
    if (var(VAR_RESULT) == WEATHER_RAIN_THUNDERSTORM) {
        msgbox(format("Ugh... it's pouring rain and my clothes are soaked, can we head back now?"), MSGBOX_DEFAULT)
    }
    else {
        msgbox(format("It's quite pleasant out here isn't it? A bit wet though...\p"
        "Let me know when you are ready to depart."), MSGBOX_DEFAULT)
    }
    
    dynmultichoice(1, 1, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Let's go", "Stay")
    switch (var(VAR_RESULT)) {
        case 0:
            call(Unova_Route17_AskHomeOrCasteliaCity)
    }
    release
}

script Unova_Route17_AskHomeOrCasteliaCity {
    msgbox(format("Do you want to head to Castelia City, or back home to Lilycove?"), MSGBOX_DEFAULT)
    dynmultichoice(1, 1, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Castelia City", "Lilycove City")
    switch (var(VAR_RESULT)) {
        case 0:
            msgbox(format("Back to Castelia City it is then! Jump on the ship and lets get going."), MSGBOX_NPC)
            call(Unova_Route17_DepartWithSailor)
            warp(MAP_CASTELIA_CITY_LIBERTY_PIER, 2)
	        waitstate
        case 1:
            msgbox(format("We've got a long voyage back to Lilycove City! All aboard!"), MSGBOX_NPC)
            call(Unova_Route17_DepartWithSailor)
            warp(MAP_LILYCOVE_CITY_HARBOR, LILYCOVE_WARP_X, LILYCOVE_WARP_Y)
	        waitstate
    }
}

script Unova_Route17_DepartWithSailor {
    applymovement(OBJECT_SAILOR, Common_Movement_WalkInPlaceFasterDown)
    waitmovement(0)
    delay(30)
    hideobjectat(OBJECT_SAILOR, MAP_UNOVA_ROUTE17)
    switch (var(VAR_FACING)) {
        case DIR_EAST: applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
        case DIR_SOUTH: applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
    }
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
    waitmovement(0)
    hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_UNOVA_ROUTE17)
    delay(30)
	call(Unova_Route17_FerryDepart)
}

script Unova_Route17_FerryDepart {
    delay(60)
    playse(SE_RG_SS_ANNE_HORN)
    applymovement(OBJECT_SSTIDAL, Unova_Route17_FerryMovement)
    waitmovement(0)
}

movement Unova_Route17_FerryMovement {
    walk_slow_left
	walk_slow_left
	walk_slow_left
	walk_left
	walk_left
	walk_left
	walk_left
}